{bracstnerf.bra
Create TEIP5-DKCLARINANNOTATION XML from token positions (XML) tokens (flat) and NER (flat).}

X=
  new
=   
  .   ~
    |   (   !inputTok
          & !inputNER
          & !uploadfileTokens
          & !output
          & get$(!inputTok,X,ML,TRM):?XMLtok
          & get$(!inputNER,STR):?ner
          & get$(!uploadfileTokens,STR):?tokens
        |   get$(IfacettokF,X ML,TRM):?XMLtok
          &     map
              $ ( ( 
                  =   
                    .   !arg:" "&
                      | !arg
                  )
                . !XMLtok
                )
            : ?XMLtok
          & get$(nerfileRAW,STR):?ner
          & get$(plaintext,STR):?tokens
          & "nerout.xml":?output
        )
      & :?nXML:?oldt:?oldA
      & 99999999999:?snold
      & ( sentNo
        =   sent
          .     @(!arg:? #%?sent "." ?)
              & str$(!sent ".")
            | 
        )
      &   vap$((=.!arg).!ner."[")
        : %?first ?ner
      & (   @( !first
             :   ?
                 ","
                 ?
                 ","
                 (certain|likely|uncertain)
                 "]"
                 ?
             )
          & !first !ner:?ner
          & :?first
        | 
        )
      & ( notting
        =   
          .   vap
            $ ( ( 
                =   
                  .     whl
                      ' @(!arg:(\n|\r) ?arg)
                    & (!arg:|(!arg.O))
                )
              . !arg
              . " "
              )
        )
      & 0:?ID
      &     notting$!first
              map
            $ ( ( 
                =   
                  .     @( !arg
                         :   (~(? "," ?):?toks)
                             ","
                             (~(? "," ?):?NE)
                             ","
                             ( certain
                             | likely
                             | uncertain
                             )
                             "]"
                             ?arg
                         )
                      & (   @(!arg:"." ?arg)
                          & str$(!toks "."):?toks
                          & (   !NE:misc
                              & person:?NE
                            | 
                            )
                        | 
                        )
                      & 1+!ID:?ID
                      &     vap
                          $ ( (=.!arg.!NE.!ID)
                            . !toks
                            . " "
                            )
                          notting$!arg
                    | notting$(str$("[" !arg))
                )
              . !ner
              )
        : ?ner
      &     vap
          $ ( ( 
              =   
                .   vap
                  $ ( ( 
                      = .vap$((=.!arg).!arg." ")
                      )
                    . !arg
                    . \r
                    )
              )
            . !tokens
            . \n
            )
        : ?tokens
      & :?ren
      & !tokens:?toks
      &   whl
        ' ( !toks:%?tok ?toks
          & !ner:(?w.?c) ?nern
          & !tok:?T
          &   whl
            ' ( @(!T:!w ?t)
              & !t:~
              &   !nern
                :   (?w2&@(!t:!w2 ?T).?)
                    ?nern
              & str$(!w !w2):?w
              )
          & (   !w:!tok
              & (!w.!c) !ren:?ren
              & !nern:?ner
            |   !ner:? [?nerL
              & !toks:? [~<!nerL
              & (!w.O) !ren:?ren
            )
          )
      & :?ner
      &   whl
        ' ( !ren:(?w.?c) ?ren
          & (!w.!c) !ner:?ner
          )
      &   whl
        ' ( !XMLtok
          : (   (?e.?a) @?A (.!e.) ?XMLtok
              &   !a
                : ? ("xml:id".?t) ?
                : ? (from.?from) ?
                : ( ? (to.?to) ?
                  | ?&!from:?to
                  )
              & (@(!from:"#" ?from)|)
              & (@(!to:"#" ?to)|)
              & !ner:%?p ?ner
              & !tokens:%?w ?tokens
              & @( rev$!w
                 : ( ?w (" "|\n|\r) ?
                   | ?w
                   )
                 )
              & low$(rev$!w):?w
              &     ( !e
                    .       @(low$!A:? !w ?oldA)
                          & (from.str$("#" !t))
                        |   @(low$!oldA:? !w ?oldA)
                          &     (!e.!a)
                                !A
                                (.!e.)
                                !XMLtok
                            : ?XMLtok
                          & (from.str$("#" !oldt))
                        | (from.str$("#" !t))
                      , !p:(?.?pos)&!pos
                    )
                    !nXML
                : ?nXML
              & !t:?oldt
            |   %?v ?XMLtok
              & (   !v:(spanGrp.ana.?)
                  & (spanGrp.ana.ner):?v
                | 
                )
              & !v !nXML:?nXML
            )
          )
      &     ( 
            =   g L
              .   :?L
                &   whl
                  ' ( !arg:%?g ?arg
                    &     ( !g:(?.?,O)&
                          |   !g:(?e.?Is,?NE)
                            & !Is:?Ie
                            &   whl
                              ' ( !arg
                                :   ((!e.?,!NE):(?.?Ie,?))
                                    ?arg
                                )
                            & !Is:? (from.?from) ?
                            &   !Ie
                              : ( ? (to.?to) ?
                                | ? (from.?to) ?
                                )
                            & !NE:(?NE.?)
                            & (   !from:!to
                                & (!e.(from.!from),!NE)
                              | ( !e
                                .   (from.!to) (to.!from)
                                  , !NE
                                )
                              )
                          | !g
                          )
                          !L
                      : ?L
                    )
                & !L
            )
          $ !nXML
        : ?nXML
      & 0:?nr
      &     map
          $ ( ( 
              =   
                .     !arg:(span.?at,?NE)
                    & ( span
                      .     ( "xml:id"
                            . str$(ne (1+!nr:?nr))
                            )
                            !at
                        , !NE
                      )
                  | !arg
              )
            . !nXML
            )
        : ?nXML
      & put$(toML$!nXML,!output,NEW);

r=
  get'"bracstnerf.bra"
& rmv$"bracstnerf.bak"
& ren$("bracstnerf.bra"."bracstnerf.bak")
&   put
  $ ( "{bracstnerf.bra
Create TEIP5-DKCLARINANNOTATION XML from token positions (XML) tokens (flat) and NER (flat).}

"
    , "bracstnerf.bra"
    , NEW
    , BIN
    )
& lst'(X,"bracstnerf.bra",APP,BIN)
& put'(\n,"bracstnerf.bra",APP,BIN)
& lst'(r,"bracstnerf.bra",APP,BIN)
& put$(str$("\nnew'" X ";\n"),"bracstnerf.bra",APP,BIN);

new'X;
