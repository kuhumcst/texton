{tei2conll.bra

Create CoNLL-U formatted output from TEI P5 input containing POS, lemma and dependency annotations.}

X=
  ( doit
  =   input output
    .   !arg:(?input.?output)
      & nestML$(get$(!input.X ML TRM)):?xml
      &   !xml
        : ( ? (TEI.?,? (text.?,?text) ?) ?
          | ? (text.?,?text) ?
          )
      & 0:?sentno
      & new$hash:?tokids
      & ( cont
        =   
          .   map
            $ ( ( 
                =   a e c
                  .     !arg:(~del:?e.?a,?c)
                      & Cont$!c
                    | !arg:(?.?)&
                    | !arg:~" "
                    | 
                )
              . !arg
              )
        )
      & ( content
        =   
          .   map
            $ ( ( 
                =   a e c j
                  .     !arg:(~del:?e.?a,?c)
                      & (     !a
                            : ? ("xml:id"|id.?) ?
                            : ( ? (join.?j) ?
                              | ?&:?j
                              )
                          & (cont$!c.!j)
                        | content$!c
                        )
                    | !arg:(?.?)&(.)
                    | !arg:~" "&(!arg.)
                    | 
                )
              . !arg
              )
        )
      & ( getIDs
        =   id n e a c
          .   !arg:(?arg.?n)
            &   whl
              ' ( !arg:? (?e.?a,?c) ?arg
                & (   !e:(w|c)
                    &   !a
                      : ? ("xml:id"|id.?id) ?
                    & (tokids..insert)$(!id.1+!n:?n)
                  | getIDs$(!c.!n):?n
                  )
                )
            & !n
        )
      & ( doLine
        =   a c e C msd pos id lemma ana to nBI
          .     !arg:(?e.?a,?c)
              & (     !a
                    : (   ? (msd.?msd) ?
                        & (   @(!msd:? "UPosTag=" ?pos)
                            & (   @(!pos:?pos "|" ?msd)
                                & ( (!msd.":"):("_".?msd)
                                  | 
                                  )
                              | "_":?msd
                              )
                          | "_":?pos
                          )
                      |   ? (pos.?pos) ?
                        & (   @(!pos:? "UPosTag=" ?pos)
                            & (   @(!pos:?pos "|" ?msd)
                                & ( (!msd.":"):("_".?msd)
                                  | 
                                  )
                              | "_":?msd
                              )
                          | "_":?msd
                          )
                      )
                    : ? ("xml:id".?id) ?
                    : ( ? (join.?join) ?
                      | ?&:?join
                      )
                  & str$(cont$!c):?C
                  &   !a
                    : ( ? (lemma.?lemma) ?
                      | ?&!C:?lemma
                      )
                  & (tokids..find)$!id:(?.?id)
                  & !linkGrp:? (!id.?ana.?to) ?
                  &   !id
                      \t
                      !C
                      \t
                      !lemma
                      \t
                      !pos
                      "\t_\t"
                      !msd
                      \t
                      !to
                      \t
                      ( (@(!ana:"ud-syn:" ?ana)|)
                      & !ana
                      )
                      "\t_\tNER="
                      ( !Type:&O
                      |   !BI:?nBI
                        & I:?BI
                        & !nBI "-" !Type
                      )
                      ( !join:
                      |   "|"
                          ( !join:right&SpaceAfter
                          | SpaceBefore
                          )
                          "=No"
                      )
                      \n
                |   !e:name
                  & !a:? (type.?type) ?
                  & DoLine$(!c.!type)
                | DoLine$(!c.!Type)
                )
            | 
        )
      & ( DoLine
        =   Type c BI
          .   !arg:(?c.?Type)
            & (!Type:|B:?BI)
            & map$(doLine.!c)
        )
      & ( write
        =   att s U V oj
          .   !arg:(?att.?s)
            & ( !att:? ("xml:id".?id) ?
              | 1+!sentno:?sentno:?id
              )
            & (tokids..insert)$(!id.0)
            & ( aggIDS
              =   
                .   map
                  $ ( ( 
                      =   id c
                        .       !arg
                              : ( w|pc
                                .     ?
                                      ("xml:id"|id.?id)
                                      ?
                                  , ?
                                )
                            & (tokids..insert)$(!id.1+!n:?n)
                          | !arg:(?.?,?c)&aggIDS$!c
                      )
                    . !arg
                    )
              )
            &   ( 
                = id n.0:?n&aggIDS$!arg
                )
              $ !s
            & !s:?U (linkGrp.?,?linkGrp) ?V
            & !U !V:?s
            &     map
                $ ( ( 
                    =   to from ana
                      .       !arg
                            : ( link
                              .     (ana.?ana)
                                    ( target
                                    . @( ?
                                       :   ?
                                           "#"
                                           ?to
                                           ( " "
                                           | \n
                                           | \r
                                           | \t
                                           )
                                           ?
                                           "#"
                                           ?from
                                       )
                                    )
                                , ?
                              )
                          & (tokids..find)$!to:(?.?to)
                          & (tokids..find)$!from:(?.?from)
                          & (!from.!ana.!to)
                        | 
                    )
                  . !linkGrp
                  )
              : ?linkGrp
            & :?oj
            &   str
              $ ( "# sent_id = "
                  !id
                  \n
                  "# text ="
                    map
                  $ ( ( 
                      =   j
                        .   !arg:(?arg.?j)
                          &   (   ( !oj:right
                                  | !j:left
                                  )
                                & !j:?oj
                                & 
                              | !j:?oj&" "
                              )
                              !arg
                      )
                    . content$!s
                    )
                  \n
                  DoLine$(!s.)
                  \n
                )
        )
      & ( makeCoNLL
        =   
          .   map
            $ ( ( 
                =   e att id
                  .     !arg:(?e.?att,?arg)
                      & (   !arg:? (linkGrp.?,?) ?
                          & write$(!att.!arg)
                        |   (   !att:? ("xml:id".?id) ?
                              & (   !e:u
                                  & "# newdoc id = " !id \n
                                |   !e:seg
                                  & "# newpar id = " !id \n
                                )
                            | 
                            )
                            makeCoNLL$!arg
                        )
                    | 
                )
              . !arg
              )
        )
      & put$(str$(makeCoNLL$!text),!output,NEW,BIN)
  )
  ( new
  =   
    .   ~
      |   ~
        &   (its.doit)
          $ ( ".\\BIG\\425-all\\ParlaMint-DK_20151_M78_2016-04-19-115.xml-425-step9.xml"
            . ".\\BIG\\step10-bracmat.conll"
            )
      |   ~
        &   (its.doit)
          $ ( ".\\BIG\\271-all\\JEB-058-74.xml-271-step9.xml"
            . ".\\BIG\\JEB-058-74.xml-271-step9-bracmat.conll"
            )
      |   ~
        &   (its.doit)
          $ ( "C:\\projects\\parlamint\\ParlaMint-DK-TEI-ana\\conllner\\ParlaMint-DK_2014-10-31-20141-M10-439.xml-1176-step11.xml"
            . "C:\\projects\\parlamint\\ParlaMint-DK-TEI-ana\\conllner\\ParlaMint-DK_2014-10-31-20141-M10.conll"
            )
      |   
        &   (its.doit)
          $ ( "C:\\projects\\parlamint\\tei2conll\\plain-all\\text-1-1196-step11.xml"
            . "C:\\projects\\parlamint\\tei2conll\\plain-all\\text-1-1196-step11.conll"
            )
      | (its.doit)$(arg$.arg$)
  );

r=
  get'("tei2conll.bra",TXT)
& rmv$(str$(tei2conll ".bak"))
& ren$("tei2conll.bra".str$(tei2conll ".bak"))
&   put
  $ ( "{tei2conll.bra

Create CoNLL-U formatted output from TEI P5 input containing POS, lemma and dependency annotations.}

"
    , "tei2conll.bra"
    , NEW
    , BIN
    )
& lst'(X,"tei2conll.bra",APP)
& put'(\n,"tei2conll.bra",APP,BIN)
& lst'(r,"tei2conll.bra",APP)
& put$(str$("\nnew'" X ";\n"),"tei2conll.bra",APP,BIN)
& ;

new'X;
