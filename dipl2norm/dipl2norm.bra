{dipl2norm.bra

Take latin diplom text in org-mode format and add column with normalised tokens.
The new column is derived from the facsimile column.
Normalisation means: 
1 Removal of white space, vertical bar ¦ and several types of parentheses and brackets
2 Conversion to lowercase
3 Conversion of v to u and j to i.
}

X=
  ( doit
  =   
    .   (trim=.str$(vap$((=.!arg).!arg." ")))
      & (spaces=.str$(vap$((=." ").!arg)))
      & ( pad
        =   s x Ls Ls L
          .   !arg:(?s.?x)
            & vap$((=.!arg).!x):? [?Lx
            & vap$((=.!arg).!s):? [?Ls
            & !Lx+-1*!Ls:?L
            & @(spaces$!x:?x [!L ?)
            & str$(!s !x)
        )
      & ( getFull
        =   A Z
          .       vap
                $ ( ( 
                    =   
                      .       !arg
                            : ( "("
                              | ")"
                              | "["
                              | "]"
                              | "{"
                              | "}"
                              | ⟨
                              | ⟩
                              | " "
                              )
                          & 
                        | !arg
                    )
                  . !arg
                  )
              : ?arg
            &   whl
              ' ( !arg:?A "-" ¦ ?Z
                & !A !Z:?arg
                )
            &   whl
              ' (!arg:?A ¦ ?Z&!A !Z:?arg)
            & !arg
        )
      & !arg:(?outp.?tok)
      &   ( 
          =   ?a
              "|"
              ?b
              "|"
              ?c
              "|"
              ?d
              "|"
              ?e
              "|"
              ?f
              "|"
              ?g
              "|"
              ?facs
              "|"
              ?dipl
              "|"
              ?h
              "|"
              ?i
              "|"
              ?j
              "|"
              ?k
              "|"
              ?l
              "|"
              ?m
              "|"
              ?n
              "|"
              ?o
              "|"
              ?p
              "|"
              ?q
          )
        : ?longpat
      &   ( 
          =   ?a
              "|"
              ?b
              "|"
              ?d
              "|"
              ?e
              "|"
              ?f
              "|"
              ?g
              "|"
              ?facs
              "|"
              ?dipl
              "|"
              ?h
              "|"
              ?i
              "|"
              ?j
              "|"
              ?k
              "|"
              ?l
              "|"
              ?m
              "|"
              ?n
              "|"
              ?o
              "|"
              ?p
              "|"
              ?q
          )
        : ?shortpat
      &     vap
          $ ( ( 
              =   w full short
                .     @( !arg
                       : ? "|" ? "|" ? "|" ? "|" ?
                       : ( !pat
                         |   ?w
                           & !shortpat:(=?pat)
                           & " ":?c
                           & !w:!pat
                         )
                       )
                    & str$(getFull$!facs):?K
                    &     pad
                        $ (   str
                            $ ( vap
                              $ ( ( 
                                  =   
                                    .   !arg:v&u
                                      | !arg:j&i
                                      | !arg
                                  )
                                . low$!K
                                )
                              )
                          . !facs
                          )
                      : ?g
                    &   str
                      $ ( !a
                          "|"
                          !b
                          "|"
                          !c
                          "|"
                          !d
                          "|"
                          !e
                          "|"
                          !f
                          "|"
                          !g
                          "|"
                          !facs
                          "|"
                          !dipl
                          "|"
                          !h
                          "|"
                          !i
                          "|"
                          !j
                          "|"
                          !k
                          "|"
                          !l
                          "|"
                          !m
                          "|"
                          !n
                          "|"
                          !o
                          "|"
                          !p
                          "|"
                          !q
                          \n
                        )
                  | str$(!arg \n)
              )
            .   :?lang
              & !longpat:(=?pat)
              & get$(!tok,STR)
            . \n
            )
        : ?diplom
      & put$(str$!diplom,!outp,NEW)
  )
  ( new
  =   
    .   ~
      |   ( arg$:?outp&arg$:?tok
          | "26.org":?tok&"26.norm.org":?outp
          )
        & (its.doit)$(!outp.!tok)
  );

r=
  get'("dipl2norm.bra",TXT)
& rmv$(str$(dipl2norm ".bak"))
& ren$("dipl2norm.bra".str$(dipl2norm ".bak"))
&   put
  $ ( "{dipl2norm.bra

Take latin diplom text in org-mode format and add column with normalised tokens.
The new column is derived from the facsimile column.
Normalisation means: 
1 Removal of white space, vertical bar ¦ and several types of parentheses and brackets
2 Conversion to lowercase
3 Conversion of v to u and j to i.
}

"
    , "dipl2norm.bra"
    , NEW
    , BIN
    )
& lst'(X,"dipl2norm.bra",APP)
& put'(\n,"dipl2norm.bra",APP,BIN)
& lst'(r,"dipl2norm.bra",APP)
& put$(str$("\nnew'" X ";\n"),"dipl2norm.bra",APP,BIN)
& ;

new'X;
