{orgconv.bra

Take diplom text in org-mode format and add values for lemma and/or PoS.
The lemma and Pos input must also be in org-mode format.
}

X=
  ( doit
  =   
    .   (trim=.str$(vap$((=.!arg).!arg." ")))
      & (spaces=.str$(vap$((=." ").!arg)))
      & ( pad
        =   s x Ls Ls L
          .   !arg:(?s.?x)
            & vap$((=.!arg).!x):? [?Lx
            & vap$((=.!arg).!s):? [?Ls
            & !Lx+-1*!Ls:?L
            & @(spaces$!x:?x [!L ?)
            & str$(!s !x)
        )
      & ( readOrg2colFile
        =   table H1 H2 H3
          .       vap
                $ ( ( 
                    =   token anno
                      .   @(!arg:"|" ?token "|" ?anno "|")
                        & (trim$!token.!anno)
                    )
                  . get$(!arg,STR)
                  . \n
                  )
              : (?H1.?H2) (?.?) ?table
            & ((!H1.trim$!H2).!table)
        )
      & ( readOrg3colFileLem
        =   table H1 H2
          .       vap
                $ ( ( 
                    =   token lemma
                      .   @(!arg:"|" ?token "|" ?lemma "|" ? "|")
                        & (trim$!token.!lemma)
                    )
                  . get$(!arg,STR)
                  . \n
                  )
              : (?H1.?H2) (?.?) ?table
            & ((!H1.trim$!H2).!table)
        )
      & ( readOrg3colFilePos
        =   table H1 H3
          .       vap
                $ ( ( 
                    =   token pos
                      .   @(!arg:"|" ?token "|" ? "|" ?pos "|")
                        & (trim$!token.!pos)
                    )
                  . get$(!arg,STR)
                  . \n
                  )
              : (?H1.?H3) (?.?) ?table
            & ((!H1.trim$!H3).!table)
        )
      & ( getFull
        =   A Z
          .       vap
                $ ( ( 
                    =   
                      .       !arg
                            : ( "("
                              | ")"
                              | "["
                              | "]"
                              | "{"
                              | "}"
                              | ⟨
                              | ⟩
                              | " "
                              )
                          & 
                        | !arg
                    )
                  . !arg
                  )
              : ?arg
            &   whl
              ' ( !arg:?A "-" ¦ ?Z
                & !A !Z:?arg
                )
            &   whl
              ' (!arg:?A ¦ ?Z&!A !Z:?arg)
            & !arg
        )
      & !arg:(?Iappnrm.?outp.?txt.?pos.?lem.?mrf)
      &   (!Iappnrm:1&norm|facs)
        : ?normOrFacs
      & :?hm
      & (   !pos:!lem:!mrf
          &   readOrg4colFilePos$!pos
            : (?.?pos:(?.?hp) ?)
          &   readOrg4colFileLem$!lem
            : (?.?lem:(?.?hl) ?)
          &   readOrg4colFileMrf$!mrf
            : (?.?morf:(?.?hm) ?)
        |   !pos:!lem
          &   readOrg3colFilePos$!pos
            : (?.?pos:(?.?hp) ?)
          &   readOrg3colFileLem$!lem
            : (?.?lem:(?.?hl) ?)
        |   readOrg2colFile$!pos:(?.?pos:(?.?hp) ?)
          & readOrg2colFile$!lem:(?.?lem:(?.?hl) ?)
          & ( !mrf:
            |   readOrg2colFile$!mrf
              : (?.?mrf:(?.?hm) ?)
            )
        )
      & spaces$!hp:?hp
      & spaces$!hl:?hl
      & spaces$!hm:?hm
      &   ( 
          =   ?a
              "|"
              ?b
              "|"
              ?c
              "|"
              ?d
              "|"
              ?e
              "|"
              ?f
              "|"
              ?norm
              "|"
              ?facs
              "|"
              ?dipl
              "|"
              ?h
              "|"
              ?i
              "|"
              ?j
              "|"
              ?k
              "|"
              ?l
              "|"
              ?m
              "|"
              ?n
              "|"
              ?o
              "|"
              ?p
          )
        : ?longpat
      &   ( 
          =   ?a
              "|"
              ?b
              "|"
              ?d
              "|"
              ?e
              "|"
              ?f
              "|"
              ?norm
              "|"
              ?facs
              "|"
              ?dipl
              "|"
              ?h
              "|"
              ?i
              "|"
              ?j
              "|"
              ?k
              "|"
              ?l
              "|"
              ?m
              "|"
              ?n
              "|"
              ?o
              "|"
              ?p
          )
        : ?shortpat
      &     vap
          $ ( ( 
              =   w full short D E
                .     @( !arg
                       : ? "|" ? "|" ? "|" ? "|" ?
                       : ( !pat
                         |   ?w
                           & !shortpat:(=?pat)
                           & " ":?c
                           & !w:!pat
                         )
                       )
                    & trim$(str$(getFull$!!normOrFacs)):?K
                    &   ( !K:&(!hl.!hp.!hm)
                        |   !pos:(?tokp.?POS) ?posn
                          & !lem:(?tokl.?LEM) ?lemn
                          & ( !mrf:(?tokm.?MRF) ?mrfn
                            | :?MRF:?mrfn
                            )
                          & (   sim$(!tokp.!K):>3/5
                              & !posn:?pos
                              & !lemn:?lem
                              & !mrfn:?mrf
                            |   !posn:(?tokp2.?POS2) ?posn2
                              & !lemn:(?tokl2.?LEM2) ?lemn2
                              & ( !mrfn:(?tokm2.?MRF2) ?mrfn2
                                | :?MRF2:?mrfn2
                                )
                              & sim$(str$(!tokp !tokp2).!K):>3/5
                              &     pad
                                  $ ( str$(" " trim$!LEM trim$!LEM2)
                                    . !LEM2
                                    )
                                : ?LEM
                              & !posn2:?pos
                              & !lemn2:?lem
                              & !mrfn2:?mrf
                            )
                          & (!LEM.!POS.!MRF)
                        | (!hl.!hp.!hm)
                        )
                      : (?D.?E.?E2)
                    & (trim$!D:|!D:?d)
                    & trim$!E:?e
                    & (trim$!E2:|!E2:?e2)
                    &   str
                      $ ( !a
                          "|"
                          !b
                          "|"
                          !c
                          "|"
                          !d
                          "|"
                          (!e:|!e !e2)
                          "|"
                          !f
                          "|"
                          !norm
                          "|"
                          !facs
                          "|"
                          !dipl
                          "|"
                          !h
                          "|"
                          !i
                          "|"
                          !j
                          "|"
                          !k
                          "|"
                          !l
                          "|"
                          !m
                          "|"
                          !n
                          "|"
                          !o
                          "|"
                          !p
                          \n
                        )
                  | str$(!arg \n)
              )
            .   :?lang
              & !longpat:(=?pat)
              & get$(!txt,STR)
            . \n
            )
        : ?diplom
      & put$(str$!diplom,!outp,NEW)
  )
  ( new
  =   
    .   ~
      |   ~
        &   (its.doit)
          $ ( 1
            . results
            . IfacettokF
            . "Ifacet_pos_tokF"
            . "Ifacet_lem_tokF"
            . "Ifacet_mrf_tokF"
            )
      |   arg$:?Iappnrm
        & arg$:?outp
        & arg$:?txt
        & arg$:?pos
        & arg$:?lem
        & (arg$:?mrf|:?mrf)
        & (its.doit)$(!Iappnrm.!outp.!txt.!pos.!lem.!mrf)
  );

r=
  get'("orgconv.bra",TXT)
& rmv$(str$(orgconv ".bak"))
& ren$("orgconv.bra".str$(orgconv ".bak"))
&   put
  $ ( "{orgconv.bra

Take diplom text in org-mode format and add values for lemma and/or PoS.
The lemma and Pos input must also be in org-mode format.
}

"
    , "orgconv.bra"
    , NEW
    , BIN
    )
& lst'(X,"orgconv.bra",APP)
& put'(\n,"orgconv.bra",APP,BIN)
& lst'(r,"orgconv.bra",APP)
& put$(str$("\nnew'" X ";\n"),"orgconv.bra",APP,BIN)
& ;

new'X;
