{anasplit.bra

Extract a single annotation group (spanGrp) from a composite TEI annotation file.}

X=
  ( doit
  =     output tmp1 tmp2 A B C D E F G H
      , xmlseg xmltok posstyle wat Wat
    .   ( find
        =   e a c wat
          .   !arg:(?arg.?wat)
            & !arg:? (?e.?a,?c) ?arg
            & (   !e:spanGrp
                & !a:? (ana.[%(!wat:? !sjt ?)) ?
                & (!e.!a,!c)
              | find$(!arg.!wat)
              | find$(!c.!wat)
              )
        )
      & !arg:(?input.?output.?wat.?posstyle)
      & nestML$(get$(!input,X ML TRM)):?xml
      &     (lem.lemma)
            (pos.pos)
            (mrf.msd morphology)
            (stx.syntax dep dependencies)
        : ? (!wat.?Wat) ?
      & find$(!xml.!Wat):?annot
      & (   !wat:pos
          & !posstyle:Uni
          &   !annot
            : ( ? (spanGrp.?a,?spans) ?
              | ? (text.?,? (spanGrp.?a,?spans) ?) ?
              |   ?
                  ( TEI
                  .   ?
                    ,   ?
                        (text.?,? (spanGrp.?a,?spans) ?)
                        ?
                  )
                  ?
              )
          &   ( spanGrp
              .   !a
                ,   map
                  $ ( ( 
                      =   a c
                        .     !arg
                            : ( span
                              .   ?a
                                , @( ?
                                   : "UPosTag=" ?c ("|" ?|)
                                   )
                              )
                          & (span.!a,!c)
                      )
                    . !spans
                    )
              )
            : ?annot
        | 
        )
      & put$(toML$!annot,!output,NEW)
  )
  ( new
  =   
    .   ~
      |     ( ~&(F."uit.xml".pos.Uni)
            | (arg$.arg$.arg$.arg$)
            )
          : (?input.?output.?wat.?posstyle)
        & (its.doit)$(!input.!output.!wat.!posstyle)
  );

r=
  get'("anasplit.bra",TXT)
& rmv$(str$(anasplit ".bak"))
& ren$("anasplit.bra".str$(anasplit ".bak"))
&   put
  $ ( "{anasplit.bra

Extract a single annotation group (spanGrp) from a composite TEI annotation file.}

"
    , "anasplit.bra"
    , NEW
    , BIN
    )
& lst'(X,"anasplit.bra",APP)
& put'(\n,"anasplit.bra",APP,BIN)
& lst'(r,"anasplit.bra",APP)
& put$(str$("\nnew'" X ";\n"),"anasplit.bra",APP,BIN)
& ;

new'X;
