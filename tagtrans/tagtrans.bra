{tagtrans.bra

Translate tags in TEI P5 DK Clarin annotation file.}

X=
  ( doit
  =   in ou od nw A att spans Z lem
    .   !arg:(?in.?lem.?ou.?la.?od.?nw)
      & (   !od:DSL
          & !nw:Menota
          & ( pos
            =   a e d cond
              .   !arg:(?a.?e:?d)
                & (     (sb.xNC.(=))
                        (præp.xIM.(=lemma$(at.!a)))
                        (præp.xAP.(=))
                        (interj.xIT.(=))
                        (adj.xAJ.(=))
                        (vb.xVB.(=))
                        (adv.xRP.(=lemma$(sum.!a)))
                        (adv.xAV.(=))
                        ( konj
                        . xCC
                        . (=lemma$(ok æller mæthen.!a))
                        )
                        (konj.xCS.(=))
                        (art.xAT.(=))
                        ( num
                        . xNO
                        . ( 
                          = lemma$(anner thrithje tolfte.!a)
                          )
                        )
                        (num.xNA.(=))
                        (pron.xDD.(=lemma$(thænne.!a)))
                        ( pron
                        . xDP
                        . (=lemma$(sin var.!a))
                        )
                        (pron.xPD.(=))
                        (prop.xNP.(=))
                        ("enklit. pron"."xPD eE".(=))
                    :   ?
                        (!e.?d.(=?cond)&!cond)
                        ?
                  | 
                  )
                & !d
            )
        | ( pos
          =   a e d cond
            .   !arg:(?a.?e:?d)
              & (     (NOUN.xNC.(=))
                      (ADP.xIM.(=lemma$(at.!a)))
                      (ADP.xAP.(=))
                      (INTJ.xIT.(=))
                      (ADJ.xAJ.(=))
                      (VERB.xVB.(=))
                      (AUX.xVB.(=))
                      (ADV.xRP.(=lemma$(sum.!a)))
                      (PART.xRP.(=))
                      (ADV.xAV.(=))
                      ( CCONJ
                      . xCC
                      . (=lemma$(ok æller mæthen.!a))
                      )
                      (SCONJ.xCS.(=))
                      (DET.xDD.(=))
                      ( NUM
                      . xNO
                      . ( 
                        = lemma$(anner thrithje tolfte.!a)
                        )
                      )
                      (NUM.xNA.(=))
                      (PRON.xDD.(=lemma$(thænne.!a)))
                      (PRON.xDP.(=lemma$(sin var.!a)))
                      (PRON.xPD.(=))
                      (PROPN.xNP.(=))
                      (PRON."xPD eE".(=))
                      (PUNCT.xUA.(=))
                      (SYM.xUA.(=))
                      (X.xUA.(=))
                  :   ?
                      (!e.?d.(=?cond)&!cond)
                      ?
                | 
                )
              & !d
          )
        )
      & (   !in:!lem
          & ( lemma
            =   A B F
              . !arg:(?A.?B)&!A:? !B ?
            )
          & get$(!in,JSN):(,?sents)
          &   put
            $ (   jsn
                $ ( 
                  ,   map
                    $ ( ( 
                        =   Sn ord
                          .   !arg:((?Sn.,?ord),)
                            & ( ( !Sn
                                .   
                                  ,   map
                                    $ ( ( 
                                        =   word a e alts
                                          .       !arg
                                                : (   (ID..?ID)
                                                    + (Word..?word)
                                                    + (lemma..?a)
                                                    + (pos..?e)
                                                  , 
                                                  )
                                              & (   (ID..!ID)
                                                  + (Word..!word)
                                                  + (lemma..!a)
                                                  + (pos..pos$(!a.!e))
                                                , 
                                                )
                                            |   !arg:(,?alts)
                                              & out$(alts !alts)
                                              & ( 
                                                ,   map
                                                  $ ( ( 
                                                      =   word a e alts
                                                        .     !arg
                                                            : (   (ID..?ID)
                                                                + ( Word
                                                                  . 
                                                                  . ?word
                                                                  )
                                                                + (lemma..?a)
                                                                + (pos..?e)
                                                              , 
                                                              )
                                                          & (   (ID..!ID)
                                                              + (Word..!word)
                                                              + (lemma..!a)
                                                              + ( pos
                                                                . 
                                                                .   pos
                                                                  $ (!a.!e)
                                                                )
                                                            , 
                                                            )
                                                      )
                                                    . !alts
                                                    )
                                                )
                                        )
                                      . !ord
                                      )
                                )
                              , 
                              )
                        )
                      . !sents
                      )
                  )
              , !ou
              , NEW
              )
        |     nestML$(get$(!in,X ML))
            : ?A (spanGrp.?att,?spans) ?Z
          &   nestML$(get$(!lem,X ML))
            : ?A (spanGrp.?att,?lems) ?Z
          & ( lemma
            =   A B F
              .   !arg:(?A.? (from.?F) ?)
                &   !lems
                  :   ?
                      (span.? (from.!F) ?,?B)
                      ?lems
                & !A:? !B ?
            )
          &   put
            $ (   toML
                $ ( !A
                    ( spanGrp
                    .   !att
                      ,     map
                          $ ( ( 
                              =   a e d
                                .   !arg:(span.?a,?e)
                                  & pos$(!a.!e):?d
                                  & (span.!a,!d)
                              )
                            . !spans
                            )
                        | !spans
                    )
                    !Z
                  )
              , !ou
              , NEW
              )
        )
  )
  ( new
  =   
    .   ~
      |   ~
        &   (its.doit)
          $ ( "470-5.org-4262-step7.json"
            . "470-5.org-4262-step7.json"
            . uit
            . da
            . DSL
            . Menotas
            )
      | (its.doit)$(arg$.arg$.arg$.arg$.arg$.arg$)
  );

r=
  get'("tagtrans.bra",TXT)
& rmv$(str$(tagtrans ".bak"))
& ren$("tagtrans.bra".str$(tagtrans ".bak"))
&   put
  $ ( "{tagtrans.bra

Translate tags in TEI P5 DK Clarin annotation file.}

"
    , "tagtrans.bra"
    , NEW
    , BIN
    )
& lst'(X,"tagtrans.bra",APP)
& put'(\n,"tagtrans.bra",APP,BIN)
& lst'(r,"tagtrans.bra",APP)
& put$(str$("\nnew'" X ";\n"),"tagtrans.bra",APP,BIN)
& ;

new'X;
