{tagmorftrans.bra

Translate tags in TEI P5 DK Clarin annotation file.}

X=
  ( doit
  =   in ou od nw A att posspans Z wrd
    .   !arg:(?in.?wrd.?mrf.?ou.?la.?od.?nw)
      & 0:?posmrf:?prons
      & ( get$uncertain:?uncertain
        | 0:?uncertain
        )
      & lst$(nw,nw,NEW,RAW)
      & (morf=.)
      & ( (!la:la|!nw:Parole)
        & ( morf
          =   from pos mo k v
            .     !arg
                : (? (from.?from) ?.?pos)
              &   !mrfspans
                :   ?
                    (span.? (from.!from) ?,?mo)
                    ?mrfspans
              &   vap
                $ ( ( 
                    =   
                      .     @(!arg:?k "=" ?v)
                          & (!k.!v)
                        | 
                    )
                  . !mo
                  . "|"
                  )
          )
        )
      &   nestML$(get$(!in,X ML))
        : ?A (spanGrp.?attag,?posspans) ?Z
      &   nestML$(get$(!wrd,X ML))
        : ?A (spanGrp.?,?words) ?Z
      &   nestML$(get$(!mrf,X ML))
        : ?A (spanGrp.?atmrf,?mrfspans) ?Z
      & ( word
        =   B F
          .       !arg
                : ? (from.@(?:"#" ?F)) ?
              &   !words
                :   ?
                    (span.? ("xml:id".!F) ?,?B)
                    ?words
              & !B
            | !arg
        )
      & ( !nw:Parole
        |     (Abbr.Yes..)
              (AdpType.Prep..xAP)
              (Aspect.(Imp.tIP.) (Perf.tPF.))
              ( Case
              .   (Abl.cB.)
                  (Acc.cA.)
                  (Dat.cD.)
                  (Gen.cG.)
                  (Loc..)
                  (Nom.cN.)
              )
              ( Degree
              .   (Abs.rS.)
                  (Cmp.rC.)
                  (Pos.rP.)
              )
              ( Gender
              .   (Fem.gF.)
                  (Masc.gM.)
                  (Neut.gN.)
              )
              ( Mood
              .   (Imp.mIP.)
                  (Ind.mIN.)
                  (Sub.mSU.)
              )
              (NumForm.Digit..)
              ( NumType
              .   (Card.xNA.)
                  (Dist..)
                  (Ord.xNO.)
              )
              (Number.(Plur.nP.) (Sing.nS.))
              ( Person
              .   (1.p1.)
                  (2.p2.)
                  (3.p3.)
              )
              (Poss.Yes..xDP)
              ( PronType
              .   (Art..xAT)
                  (Dem..xDD)
                  (Dem,Prs..xDDxPE)
                  (Ind..xPI)
                  (Ind,Int..xPIxPQ)
                  (Ind,Rel..xPIxRP)
                  (Prs..xPE)
                  (Rel..xRP)
              )
              (Reflex.Yes.vR.)
              ( Tense
              .   (Fut.tFS.)
                  (Imp.tPT.)
                  (Past.tPT.)
                  (Pres.tPS.)
              )
              ( VerbForm
              .   (Fin.fF.)
                  (Gdv..)
                  (Ger..)
                  (Inf.fI.)
                  (Part.fP.)
              )
              ( VerbType
              .   (Mod..)
                  (Voice.(Act.vA.) (Pass.vP.))
              )
          : ?tab
        )
      &     map
          $ ( ( 
              =   a e d mms
                .   !arg:(span.?a,?pos)
                  & (   morf$(!a.!e):?mrf
                      & (   !nw:Parole
                          & word$!a:?Word
                          & !pos\L!mrf+!posmrf:?posmrf
                          &   (   !pos:ADJ
                                & (     !mrf
                                      : ? (Case.Gen) ?
                                    & "ADJ_GEN"
                                  | ADJ
                                  )
                              |   !pos:ADP
                                & (     !word
                                      : (end|som)
                                    & UKONJ
                                  | PRÆP
                                  )
                              | !pos:ADV
                              |   !pos:(AUX|VERB)
                                & (     !mrf
                                      : ? (VerbForm.Ger) ?
                                    & "V_GERUND"
                                  |     !mrf
                                      : ? (Mood.Imp) ?
                                    & "V_IMP"
                                  |     !mrf
                                      : ? (Voice.Pass) ?
                                    & (     !mrf
                                          :   ?
                                              (VerbForm.Inf)
                                              ?
                                        & "V_INF_PAS"
                                      |     !mrf
                                          : ? (Tense.Pres) ?
                                        & "V_PRES_PAS"
                                      |     !mrf
                                          : ? (Tense.Past) ?
                                        & "V_PAST_PAS"
                                      )
                                  |     !mrf
                                      : ? (Tense.Past) ?
                                    & (     !mrf
                                          :   ?
                                              (VerbForm.Part)
                                              ?
                                        & (   @(!Word:? es)
                                            & "V_MED_PARTC_PAST"
                                          | "V_PARTC_PAST"
                                          )
                                      |   @(!Word:? es)
                                        & "V_MED_PAST"
                                      | "V_PAST"
                                      )
                                  |     !mrf
                                      : ? (Tense.Pres) ?
                                    & (     !mrf
                                          :   ?
                                              (VerbForm.Part)
                                              ?
                                        & "V_PARTC_PRES"
                                      |   @(!Word:? es)
                                        & "V_MED_PRES"
                                      | "V_PRES"
                                      )
                                  |     !mrf
                                      : ? (VerbForm.Inf) ?
                                    & (   @( !Word
                                           : ( ? es
                                             | ? ås
                                             )
                                           )
                                        & "V_MED_INF"
                                      | "V_INF"
                                      )
                                  )
                              | !pos:CCONJ&SKONJ
                              |   !pos:DET
                                & (     !mrf
                                      : ? (Case.Gen) ?
                                    & ens
                                    & "PRON_UBST_GEN_S"
                                  |     !mrf
                                      : ? (Poss.Yes) ?
                                    & (     !mrf
                                          : ? (Number.Sing) ?
                                        & (     !mrf
                                              :   ?
                                                  (Gender.Com)
                                                  ?
                                            & (     !mrf
                                                  :   ?
                                                      (Person.1)
                                                      ?
                                                & min
                                                & "PRON_POSS_1S"
                                              |     !mrf
                                                  :   ?
                                                      (Person.2)
                                                      ?
                                                & din
                                                & "PRON_POSS_2S"
                                              |     !mrf
                                                  :   ?
                                                      (Person.3)
                                                      ?
                                                & sin
                                                & "PRON_POSS_3S"
                                              )
                                          |     !mrf
                                              :   ?
                                                  (Gender.Neut)
                                                  ?
                                            & (     !mrf
                                                  :   ?
                                                      (Person.1)
                                                      ?
                                                & mit
                                                & "PRON_POSS_1S"
                                              |     !mrf
                                                  :   ?
                                                      (Person.2)
                                                      ?
                                                & dit
                                                & "PRON_POSS_2S"
                                              |     !mrf
                                                  :   ?
                                                      (Person.3)
                                                      ?
                                                & sit
                                                & "PRON_POSS_3S"
                                              )
                                          )
                                      |     !mrf
                                          : ? (Number.Plur) ?
                                        & (     !mrf
                                              :   ?
                                                  (Person.1)
                                                  ?
                                            & (     !mrf
                                                  :   ?
                                                      (Number[psor].Plur)
                                                      ?
                                                & vore
                                                & "PRON_POSS_1P"
                                              |     !mrf
                                                  :   ?
                                                      (Number[psor].Sing)
                                                      ?
                                                & mine
                                                & "PRON_POSS_1P"
                                              )
                                          |     !mrf
                                              :   ?
                                                  (Person.2)
                                                  ?
                                            & dine
                                            & "PRON_POSS_2P"
                                          |     !mrf
                                              :   ?
                                                  (Person.3)
                                                  ?
                                            &   !mrf
                                              :   ?
                                                  (Number[psor].Sing)
                                                  ?
                                            & sine
                                            & "PRON_POSS_3P"
                                          )
                                      |     !mrf
                                          : ? (Person.1) ?
                                        & vores
                                        & "PRON_POSS_1"
                                      |     !mrf
                                          : ? (Person.2) ?
                                        & jeres
                                        & "PRON_POSS_2"
                                      |     !mrf
                                          : ? (Person.3) ?
                                        & (     !mrf
                                              :   ?
                                                  (Number[psor].Plur)
                                                  ?
                                            & (   !Word:Deres
                                                & "PRON_POSS_2"
                                              |   !Word:deres
                                                & "PRON_POSS_3"
                                              )
                                          |     !mrf
                                              :   ?
                                                  (Number[psor].Sing)
                                                  ?
                                            & hendes
                                            & "PRON_POSS_3"
                                          )
                                      )
                                  |     !mrf
                                      : ? (PronType.Dem) ?
                                    & (     !mrf
                                          : ? (Number.Sing) ?
                                        & (     !mrf
                                              :   ?
                                                  (Gender.Com)
                                                  ?
                                            & den
                                            & "PRON_DEMO_S"
                                          |     !mrf
                                              :   ?
                                                  (Gender.Neut)
                                                  ?
                                            & det
                                            & "PRON_DEMO_S"
                                          )
                                      |     !mrf
                                          : ? (Number.Plur) ?
                                        & "PRON_DEMO_P"
                                      | selv&"PRON_DEMO"
                                      )
                                  |     !mrf
                                      : ? (PronType.Ind) ?
                                    & (     !mrf
                                          : ? (Number.Sing) ?
                                        & (     !mrf
                                              :   ?
                                                  (Gender.Com)
                                                  ?
                                            & een en enhver hvert
                                            & "PRON_UBST_S"
                                          |     !mrf
                                              :   ?
                                                  (Gender.Neut)
                                                  ?
                                            &   alting
                                                et
                                                ethvert
                                                noget
                                            & "PRON_UBST_S"
                                          )
                                      |     !mrf
                                          : ? (Number.Plur) ?
                                        & nogle
                                        & "PRON_UBST_P"
                                      )
                                  )
                              |   !pos:INTJ
                                &   Ja
                                    Jo
                                    a
                                    ah
                                    ja
                                    jo
                                    nej
                                    værsgo
                                & INTERJ
                              |   !pos:NOUN
                                & (     (     !mrf
                                            :   ?
                                                (Definite.Def)
                                                ?
                                          & "_DEF"
                                        |     !mrf
                                            :   ?
                                                (Definite.Ind)
                                                ?
                                          & "_INDEF"
                                        | 
                                        )
                                      : ?definite
                                    &   (     !mrf
                                            :   ?
                                                (Number.Plur)
                                                ?
                                          & "_PLU"
                                        |     !mrf
                                            :   ?
                                                (Number.Sing)
                                                ?
                                          & "_SING"
                                        )
                                      : ?number
                                    &   (     !mrf
                                            : ? (Case.Gen) ?
                                          & "_GEN"
                                        | 
                                        )
                                      : ?case
                                    &   str
                                      $ (N !definite !number !case)
                                  | "N_SING"
                                  )
                              |   !pos:NUM
                                & (     !mrf
                                      : ? (NumType.Card) ?
                                    & NUM
                                  | "NUM_ORD"
                                  )
                              |   !pos:PART
                                &   !mrf
                                  : ? (PartType.Inf) ?
                                & at
                                & UNIK
                              |   !pos:PRON
                                & (     !mrf
                                      : ? (PronType.Prs) ?
                                    & (     !mrf
                                          : ? (Case.Nom) ?
                                        &   !mrf
                                          : ? (Gender.Com) ?
                                        &   !mrf
                                          : ? (Number.Plur) ?
                                        &   !mrf
                                          : ? (Person.1) ?
                                        & vi
                                        & "PRON_PERS_1P"
                                      |     !mrf
                                          : ? (Case.Acc) ?
                                        & (     !mrf
                                              :   ?
                                                  (Gender.Neut)
                                                  ?
                                            &   !mrf
                                              :   ?
                                                  (Number.Sing)
                                                  ?
                                            &   !mrf
                                              :   ?
                                                  (Person.3)
                                                  ?
                                            & det
                                            & "PRON_PERS_3S"
                                          |     !mrf
                                              :   ?
                                                  (Gender.Com)
                                                  ?
                                            & (     !mrf
                                                  :   ?
                                                      (Number.Sing)
                                                      ?
                                                & (     !mrf
                                                      :   ?
                                                          (Person.1)
                                                          ?
                                                    & mig
                                                    & "PRON_PERS_1S"
                                                  |     !mrf
                                                      :   ?
                                                          (Person.2)
                                                          ?
                                                    & !Word:dig
                                                    & "PRON_PERS_2S"
                                                  |     !mrf
                                                      :   ?
                                                          (Person.3)
                                                          ?
                                                    & ham hende
                                                    & "PRON_PERS_3S"
                                                  )
                                              |     !mrf
                                                  :   ?
                                                      (Number.Plur)
                                                      ?
                                                & (     !mrf
                                                      :   ?
                                                          (Person.1)
                                                          ?
                                                    & os
                                                    & "PRON_PERS_1P"
                                                  |     !mrf
                                                      :   ?
                                                          (Person.2)
                                                          ?
                                                    & jer
                                                    & "PRON_PERS_2P"
                                                  )
                                              )
                                          |     !mrf
                                              :   ?
                                                  (Person.3)
                                                  ?
                                            & (     !mrf
                                                  :   ?
                                                      (Number.Plur)
                                                      ?
                                                & dem
                                                & "PRON_PERS_3P"
                                              |     !mrf
                                                  :   ?
                                                      (Reflex.Yes)
                                                      ?
                                                & sig
                                                & "PRON_PERS_3"
                                              )
                                          )
                                      |     !mrf
                                          : ? (Case.Nom) ?
                                        & (     !mrf
                                              :   ?
                                                  (Person.1)
                                                  ?
                                            & jeg
                                            & "PRON_PERS_1S"
                                          |     !mrf
                                              :   ?
                                                  (Person.2)
                                                  ?
                                            & du
                                            & "PRON_PERS_2S"
                                          |     !mrf
                                              :   ?
                                                  (Person.3)
                                                  ?
                                            & han hun
                                            & "PRON_PERS_3S"
                                          )
                                      |     !mrf
                                          : ? (Poss.Yes) ?
                                        & (     !mrf
                                              :   ?
                                                  (Number.Sing)
                                                  ?
                                            & (     !mrf
                                                  :   ?
                                                      (Gender.Com)
                                                      ?
                                                & (     !mrf
                                                      :   ?
                                                          (Person.1)
                                                          ?
                                                    & (     !mrf
                                                          :   ?
                                                              ( Number[psor]
                                                              . Sing
                                                              )
                                                              ?
                                                        & !Word:min
                                                        & "PRON_POSS_1S"
                                                      |     !mrf
                                                          :   ?
                                                              ( Number[psor]
                                                              . Plur
                                                              )
                                                              ?
                                                        & !Word:mine
                                                        & "PRON_POSS_1P"
                                                      )
                                                  |     !mrf
                                                      :   ?
                                                          (Person.3)
                                                          ?
                                                      :   ?
                                                          ( Number[psor]
                                                          . Sing
                                                          )
                                                          ?
                                                      :   ?
                                                          (Reflex.Yes)
                                                          ?
                                                    & !Word:sin
                                                    & "PRON_POSS_3S"
                                                  )
                                              |     !mrf
                                                  :   ?
                                                      (Gender.Neut)
                                                      ?
                                                  :   ?
                                                      (Number[psor].Sing)
                                                      ?
                                                  :   ?
                                                      (Person.3)
                                                      ?
                                                  :   ?
                                                      (Reflex.Yes)
                                                      ?
                                                & !Word:sit
                                                & "PRON_POSS_3S"
                                              )
                                          |     !mrf
                                              :   ?
                                                  (Number.Plur)
                                                  ?
                                              :   ?
                                                  (Number[psor].Sing)
                                                  ?
                                              :   ?
                                                  (Person.3)
                                                  ?
                                              :   ?
                                                  (Reflex.Yes)
                                                  ?
                                            & !Word:sine
                                            & "PRON_POSS_3P"
                                          |     !mrf
                                              :   ?
                                                  (Number[psor].Sing)
                                                  ?
                                              :   ?
                                                  (Person.3)
                                                  ?
                                            & !Word:hendes
                                            & "PRON_POSS_3"
                                          )
                                      |     !mrf
                                          : ? (Person.2) ?
                                          : ? (Polite.Form) ?
                                          : ? (Poss.Yes) ?
                                        & (   !Word:deres
                                            & "PRON_POSS_3"
                                          |   !Word:jeres
                                            & "PRON_POSS_2"
                                          )
                                      )
                                  |     !mrf
                                      : ? (PronType.Dem) ?
                                    & (     !mrf
                                          : ? (Number.Plur) ?
                                        & de
                                        & "PRON_DEMO_P"
                                      |     !mrf
                                          : ? (Number.Sing) ?
                                        & den
                                        & "PRON_DEMO_S"
                                      | selv&"PRON_DEMO"
                                      )
                                  |     !mrf
                                      : ? (PronType.Rcp) ?
                                    &   !mrf
                                      : ? (Number.Plur) ?
                                    & hinanden
                                    & "PRON_REC"
                                  |     !mrf
                                      : ? (PronType.Ind) ?
                                    & (     !mrf
                                          : ? (Gender.Com) ?
                                        & anden hver ingen nogen
                                        & "PRON_UBST_S"
                                      |     !mrf
                                          : ? (Gender.Neut) ?
                                        &   andet
                                            ingenting
                                            intet
                                            noget
                                        & "PRON_UBST_S"
                                      |     !mrf
                                          : ? (Number.Plur) ?
                                        & andre
                                        & "PRON_UBST_P"
                                      )
                                  |     !mrf
                                      :   ?
                                          (PronType."Int,Rel")
                                          ?
                                    & hvad hvilken hvilket
                                    & "PRON_INTER_REL_S"
                                  |     !mrf
                                      : ? (PartType.Inf) ?
                                    & der som
                                    & UNIK
                                  )
                              |   !pos:PROPN
                                & (     !mrf
                                      : ? (Case.Gen) ?
                                    & "EGEN_GEN"
                                  | EGEN
                                  )
                              |   !pos:SCONJ
                                & (     !Word
                                      : ( at
                                        | da
                                        | dengang
                                        | efter
                                        | eftersom
                                        | end
                                        | endskønt
                                        | enten
                                        | fordi
                                        | før
                                        | førend
                                        | hvis
                                        | hvorvidt
                                        | idet
                                        | imens
                                        | inden
                                        | indtil
                                        | ~<>jo
                                        | ligesom
                                        | medens
                                        | medmindre
                                        | mens
                                        | når
                                        | om
                                        | omend
                                        | qua
                                        | selvom
                                        | siden
                                        | skønt
                                        | så
                                        | såfremt
                                        | såsom
                                        | til
                                        )
                                    & UKONJ
                                  |     !Word
                                      : ( altså
                                        | bagefter
                                        | "bl.a"
                                        | cirka
                                        | derfra
                                        | eksempelvis
                                        | fra
                                        | givetvis
                                        | henholdsvis
                                        | hvadenten
                                        | hvorend
                                        | hvornår
                                        | hvorom
                                        | hvortil
                                        | hvv
                                        | langtfra
                                        | muligvis
                                        | naturligvis
                                        | nødvendigvis
                                        | også
                                        | straks
                                        | vidt
                                        )
                                    & ADV
                                  | ~&SKONJ
                                  )
                              | !pos:PUNCT&TEGN
                              | !pos:SYM&SYMBOL
                              |   !pos:X
                                & (     !morf
                                      : ? (Abbr.Yes) ?
                                    & FORK
                                  | XX
                                  )
                              |     !pos\L(!mrf^!Word)+!uncertain
                                  : ?uncertain
                                & N
                              )
                            : ?POS
                          & (   @(!POS:PRON ?)
                              &   !POS\L(!mrf^!Word)+!prons
                                : ?prons
                            | 
                            )
                          & (!a.!POS.)
                        |   :?pp
                          & 0:?mms
                          &   map
                            $ ( ( 
                                =   k v M P s C
                                  .     !arg:(?k.?v)
                                      &   !tab
                                        :   ?
                                            ( !k
                                            .   ?
                                                ( !v
                                                . @(?:%@?s ?C)
                                                . ?P
                                                )
                                                ?
                                            )
                                            ?
                                      & ( !P:
                                        | !P:?pp
                                        )
                                      & !s\L!C+!mms:?mms
                                      & 
                                    | 
                                )
                              . !mrf
                              )
                          & (     mop
                                $ ( ( 
                                    =   
                                      .   !arg:?s\L?Cs
                                        &   " "
                                              str
                                            $ ( !s
                                                  mop
                                                $ ( ( 
                                                    =   
                                                      .     !arg
                                                          : %@?C^?
                                                        & !C
                                                    )
                                                  . !Cs
                                                  . (=*)
                                                  )
                                              )
                                    )
                                  . !mms
                                  . (=+)
                                  )
                              : " " ?mm
                            | :?mm
                            )
                          & (   !pp:
                              &     (ADJ.xAJ)
                                    (ADP.xAP)
                                    (ADV.xAV)
                                    (AUX.xVB)
                                    (CCONJ.xCC)
                                    (DET.xDD)
                                    (INTJ.xIT)
                                    (NOUN.xNC)
                                    (NUM.xNA)
                                    (PART.xRP)
                                    (PRON.xPD)
                                    (PROPN.xNP)
                                    (PUNCT.xUA)
                                    (SCONJ.xCS)
                                    (SYM.xUA)
                                    (VERB.xVB)
                                    (X.xUA)
                                : ? (!pos.?pp) ?
                              & (   !pp:
                                  & xUA:?pp
                                | 
                                )
                            | 
                            )
                          & !pos\L!mrf+!posmrf:?posmrf
                          & (   ~
                              &   !pos
                                : ( ADJ
                                  | NOUN
                                  | VERB
                                  )
                            |   !pos\L(!mrf^word$!a)+!uncertain
                              : ?uncertain
                            | 
                            )
                          & (   !nw:Parole
                              & V:?pp
                            | 
                            )
                          & (!a.!pp.!mm)
                        )
                    | 
                    )
              )
            . !posspans
            )
        : ?posmorfs
      & lst$(uncertain,uncertain,NEW,RAW,WYD)
      &   ( spanGrp
          .   !attag
            ,   map
              $ ( ( 
                  =   
                    .   !arg:(?a.?arg.?)
                      & (span.!a,!arg)
                  )
                . !posmorfs
                )
          )
        : ?posann
      &   ( !nw:Parole&!posann
          | ( TEI
            .   (xmlns."http://www.tei-c.org/ns/1.0")
              ,   (teiHeader.,)
                  ( text
                  .   
                    ,   !A
                        !posann
                        ( spanGrp
                        .   !atmrf
                          ,   map
                            $ ( ( 
                                =   
                                  .   !arg:(?a.?.?arg)
                                    & (span.!a,!arg)
                                )
                              . !posmorfs
                              )
                        )
                        !Z
                  )
            )
          )
        : ?tei
      & put$(toML$!tei,!ou,NEW)
  )
  ( new
  =   
    .   ~
      |   ~
        &   (its.doit)
          $ ( "470-5.org-4262-step7.json"
            . "470-5.org-4262-step7.json"
            . uit
            . da
            . DSL
            . Menotas
            )
      |   ~
        &   (its.doit)
          $ ( IfacetposF
            . IfacetlemF
            . IfacetmrfF
            . tagtrans-results
            . la
            . Uni
            . Uni
            . Menota
            . Menota
            )
      |   lst$(arg,arg,NEW,RAW)
        &   (its.doit)
          $ ( arg$
            . arg$
            . arg$
            . arg$
            . arg$
            . arg$
            . arg$
            )
  );

r=
  get'("tagmorftrans.bra",TXT)
& rmv$(str$(tagmorftrans ".bak"))
& ren$("tagmorftrans.bra".str$(tagmorftrans ".bak"))
&   put
  $ ( "{tagmorftrans.bra

Translate tags in TEI P5 DK Clarin annotation file.}

"
    , "tagmorftrans.bra"
    , NEW
    , BIN
    )
& lst'(X,"tagmorftrans.bra",APP)
& put'(\n,"tagmorftrans.bra",APP,BIN)
& lst'(r,"tagmorftrans.bra",APP)
& put$(str$("\nnew'" X ";\n"),"tagmorftrans.bra",APP,BIN)
& ;

new'X;
