{braposf.bra
Create TEIP5-DKCLARINANNOTATION XML from token positionss (XML) tokens (flat) and PoS (flat).}

X=
  new
=   
  .   ~
    |   ( 
        |   put
          $ (   inputTok
                !inputTok
                inputPos
                !inputPos
                uploadfileTokens
                !uploadfileTokens
                output
                !output
                \n
            , "braposf.log"
            , NEW
            )
        )
      & !inputTok
      & !inputPos
      & !uploadfileTokens
      & !output
      & ( get$(!inputTok,X,ML,TRM):?XMLtok
        | 
        | put$(cannotopen !inputTok \n,"braposf.log",APP)
        )
      & ( get$(!inputPos,STR):?pos
        | 
        | put$(cannotopen !inputPos \n,"braposf.log",APP)
        )
      & ( get$(!uploadfileTokens,STR):?tokens
        | 
        | put$(cannotopen !uploadfileTokens \n,"braposf.log",APP)
        )
      & :?nXML:?oldt:?oldA
      & 0:?nr
      & 99999999999:?snold
      & ( sentNo
        =   sent
          .   @(!arg:? #%?sent "." ?)&str$(!sent ".")
            | 
        )
      & ( units
        =   
          .   vap
            $ ( (=.vap$((=.vap$(.!arg." ")).!arg.\r))
              . !arg
              . \n
              )
        )
      & units$!pos:?pos
      & units$!tokens:?tokens
      &   whl
        ' ( !XMLtok
          : (   (?e.?a) @?A (.!e.) ?XMLtok
              &   !a
                : ? ("xml:id".?t) ?
                : ? (from.?from) ?
                : ( ? (to.?to) ?
                  | ?&!from:?to
                  )
              & (@(!from:"#" ?from)|)
              & (@(!to:"#" ?to)|)
              & !pos:%?p ?pos
              & !tokens:%?w ?tokens
              &     ( !e
                    .       @(!A:? ~<>!w ?oldA)
                          & sentNo$!t:?sno
                          & (!sno:!snold|0:?nr)
                          &   ("xml:id".str$(p !sno (1+!nr:?nr)))
                              (from.str$("#" !t))
                        |   @(!oldA:? ~<>!w ?oldA)
                          & sentNo$!oldt:?sno
                          & (!sno:!snold|0:?nr)
                          &   (!e.!a) !A (.!e.) !XMLtok
                            : ?XMLtok
                          &   ("xml:id".str$(p !sno (1+!nr:?nr)))
                              (from.str$("#" !oldt))
                      , !p
                    )
                    !nXML
                : ?nXML
              & !t:?oldt
              & !sno:?snold
            |   %?v ?XMLtok
              & (   !v:(spanGrp.ana.?)
                  & (spanGrp.ana.pos):?v
                | 
                )
              & !v !nXML:?nXML
            )
          )
      &     ( 
            =   g L
              .   :?L
                & whl'(!arg:%?g ?arg&!g !L:?L)
                & !L
            )
          $ !nXML
        : ?nXML
      & put$(toML$!nXML,!output,NEW);

r=
  get'"braposf.bra"
& rmv$"braposf.bak"
& ren$("braposf.bra"."braposf.bak")
&   put
  $ ( "{braposf.bra
Create TEIP5-DKCLARINANNOTATION XML from token positionss (XML) tokens (flat) and PoS (flat).}

"
    , "braposf.bra"
    , NEW
    , BIN
    )
& lst'(X,"braposf.bra",APP,WYD,BIN)
& put'(\n,"braposf.bra",APP,BIN)
& lst'(r,"braposf.bra",APP,WYD,BIN)
& put$(str$("\nnew'" X ";\n"),"braposf.bra",APP,BIN);

new'X;
