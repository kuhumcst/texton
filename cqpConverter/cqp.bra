{cqp.bra
Combine standoff annotations for tokens, part of speech and lemmas into VRT (CQP) format

e.g.

<text title="Murværk opmulet med vådmørtel august" date= "20170830" datefrom="20170830" dateto="20170830" timefrom="000000" timeto="235959" >
<p idp ="1">
<sentence id ="1">
Murværk         N         N.IND.SING  Murværk
opmuret           V         V.PARTC.PAST        opmure
med     PREP  PREP  med
vådmørtler       N         N.IND.PLU    vådmørtel
</sentence>
}
X=
  ( doit
  =   
    .     !arg
        : (?tok.?seg.?pos.?lem.?segtok.?outp.?today)
      & :?past
      &   ( !segtok:
          | get$(!segtok,X ML TRM)
          )
        : ?ST
      & "segtok: This file is the original TEI P5 text. The file is read without nesting it afterwards!
         It is scanned for <lb> and <pb> markings, so they can be reproduced in the output."
      &     map
          $ ( ( 
              =   
                .       !arg
                      : ((lb|pb):?arg.?)
                    & (!arg.!past):?arg
                    & :?past
                    & !arg
                  |     !arg
                      : ( c|w
                        . ? ("xml:id".?arg) ?
                        )
                    & !arg !past:?past
                    & 
                  | 
              )
            . !ST
            )
        : ?lpb
      & ( !tok:
        |   get$(!tok,X ML):?T
          &   ( !seg:
              | get$(!seg,X ML TRM)
              )
            : ?S
          &   ( !pos:
              | get$(!pos,X ML TRM)
              )
            : ?P
          &   ( !lem:
              | get$(!lem,X ML TRM)
              )
            : ?L
          & 0:?ID
          & :?p:?words
          & new$hash:?PH
          & new$hash:?LH
          & new$hash:?SH
          &   map
            $ ( ( 
                =   
                  .     !arg
                      : ( ( span
                          .   ?
                              (from.@(?:"#" ?ref))
                              ?
                          )
                        |   %@?p
                          & (PH..insert)$(!ref.!p)
                        )
                    | 
                )
              . !P
              )
          &   map
            $ ( ( 
                =   
                  .     !arg
                      : ( ( span
                          .   ?
                              (from.@(?:"#" ?ref))
                              ?
                          )
                        |   %@?l
                          & (LH..insert)$(!ref.!l)
                        )
                    | 
                )
              . !L
              )
          &   map
            $ ( ( 
                =   
                  .       !arg
                        : ( span
                          .     ?
                                (to.@(?:"#" ?ref))
                                ?
                            , ?
                          )
                      & (SH..insert)$(!ref.)
                    | 
                )
              . !S
              )
          &   whl
            ' (   !T
                :   ?
                    ( span
                    .   ? ("xml:id".?tid) ?
                      : ? (from.@(?:"#" ?fid)) ?
                    )
                    ?token
                    (.span.)
                    ?T
              & str$("#" !tid):?ref
              & ( (PH..find)$!tid:(?.?POS)
                | "_":?POS
                )
              & ( (LH..find)$!tid:(?.?lemma)
                | "_":?lemma
                )
              & ( !('(!lpb:(?.? ()$fid ?) ?))
                |   !lpb:(?lp.?) ?lpb
                  & \n (!lp.,) !words:?words
                | 
                )
              &       str
                    $ ( !token
                        \t
                        ( ( @(!POS:?cl "_" ?)
                          | !POS:?cl
                          )
                        & !cl
                        )
                        \t
                        !POS
                        \t
                        !lemma
                        \n
                      )
                    !words
                : ?words
              & :?sent
              & (   (SH..find)$!tid
                  & 1+!ID:?ID
                  &   whl
                    ' ( !words:%?line ?words
                      & !line !sent:?sent
                      )
                  &     \n
                        (sentence.(id.!ID),\n !sent)
                        \n
                        !p
                    : ?p
                | 
                )
              )
          & :?PP
          &   whl
            ' ( !p:%?elm ?p
              & !elm !PP:?PP
              )
          & !PP:?p
          &     toML
              $ ( ("?"."xml version=\"1.0\" encoding=\"UTF-8\"")
                  \n
                  ( text
                  .     (title.TITLE)
                        (date.!today)
                        (datefrom.!today)
                        (dateto.!today)
                        (timefrom.000000)
                        (timeto.235959)
                    , \n (p.(idp.1),!p) \n\n
                  )
                )
            : ?ML
          & @(!ML:? \n ?ML)
          & put$(!ML,!outp,NEW,BIN)
        )
  )
  ( new
  =   
    .   ~
      |   (arg$:?outp|:?outp)
        & (arg$:?tok|:?tok)
        & (arg$:?seg|:?seg)
        & (arg$:?pos|:?pos)
        & (arg$:?lem|:?lem)
        & (arg$:?segtok|:?segtok)
        & (arg$:?today|:?today)
        &   (its.doit)
          $ (!tok.!seg.!pos.!lem.!segtok.!outp.!today)
  );

r=
  get'"cqp.bra"
& rmv$(str$(cqp ".bak"))
& ren$("cqp.bra".str$(cqp ".bak"))
&   put
  $ ( "{cqp.bra
Combine standoff annotations for tokens, part of speech and lemmas into VRT (CQP) format

e.g.

<text title=\"Murværk opmulet med vådmørtel august\" date= \"20170830\" datefrom=\"20170830\" dateto=\"20170830\" timefrom=\"000000\" timeto=\"235959\" >
<p idp =\"1\">
<sentence id =\"1\">
Murværk         N         N.IND.SING  Murværk
opmuret           V         V.PARTC.PAST        opmure
med     PREP  PREP  med
vådmørtler       N         N.IND.PLU    vådmørtel
</sentence>
}
"
    , "cqp.bra"
    , NEW
    , BIN
    )
& lst'(X,"cqp.bra",APP,BIN)
& put'(\n,"cqp.bra",APP,BIN)
& lst'(r,"cqp.bra",APP,BIN)
& put$(str$("\nnew'" X ";\n"),"cqp.bra",APP,BIN);

new'X;
