{f2b.bra

Take segmentized and tokenized text with DEL as separator between 'tokens' that belong to the same input word apart.
Output a <text> element in TEI-P5 style.}

X=
  ( doit
  =   
    .   0:?seg
      & chr$127:?DEL
      & ( inv
        =   L a
          .   :?L
            &   whl
              ' ( !arg:%?a ?arg
                & !a !L:?L
                )
            & !L
        )
      & ( FF
        =   x y
          .   :?y
            &   whl
              ' ( @( !arg
                   :   ?x
                       (%@:(\f|\v):?fv)
                       ?arg
                   )
                &     (   !fv:\f&pb
                        | lb
                      . ,
                      )
                      ( !x:
                      | (w.(S.!seg) (T.!tok),!x)
                      )
                      !y
                  : ?y
                & 1+!tok:?tok
                )
            &   ( !arg:
                | (w.(S.!seg) (T.!tok),!arg)
                )
                !y
        )
      & ( cut
        =   W a
          .   :?W
            &   whl
              ' ( @(!arg:?a "." ?arg)
                & FF$!a:?a
                &     ( c
                      .     (S.!seg)
                            (T.!tok)
                            (type.p)
                        , "."
                      )
                      !a
                      !W
                  : ?W
                )
            & FF$!arg !W
        )
      & ( CUT
        =   WW
          .       vap
                $ ( ( 
                    =   
                      .       !arg
                            : ( "'"
                              | "("
                              | ")"
                              | "["
                              | "]"
                              | "{"
                              | "}"
                              | ":"
                              | ","
                              | "-"
                              | –
                              | —
                              | …
                              | "!"
                              | ‹
                              | ›
                              | «
                              | »
                              | "?"
                              | ‘
                              | ’
                              | “
                              | ”
                              | ";"
                              | "\\"
                              | "/"
                              | \"
                              | "&"
                              )
                          & (p.!arg)
                        | !arg
                    )
                  . !arg
                  )
              : ?C
            &   whl
              ' (   !C
                  : ?A (p.%@?x ?y) (p.!x) ?Z
                & !A (p.!x !y !x) !Z:?C
                )
            & :?O
            &   whl
              ' ( !C:?A (p.?x) ?C
                & ( !A:
                  |   1+!tok:?tok
                    & cut$(str$!A):?A
                  )
                &     ( c
                      .     (S.!seg)
                            (T.!tok)
                            (type.p)
                        , str$!x
                      )
                      !A
                      !O
                  : ?O
                )
            & ( !A:
              | str$!C:?C:
              | cut$!C !O:?O
              )
            & :?OO
            &   whl
              ' (!O:%?A ?O&!A !OO:?OO)
            & 1+!tok:?tok
            & !OO
        )
      & ( reindex
        =   tok prefix i
          .   0:?tok
            & !arg:(?arg.?prefix)
            & 0:?i
            &   map
              $ ( ( 
                  =   e a c
                    .     !arg:(?e.?a,?c)
                        & (   !e:(w|c)
                            &     map
                                $ ( ( 
                                    =   
                                      .       !arg
                                            : (   "xml:id"
                                                | T
                                                | S
                                              . ?
                                              )
                                          & 
                                        | !arg
                                    )
                                  . !a
                                  )
                              : ?a
                            &     ( "xml:id"
                                  .   str
                                    $ (!prefix (1+!tok:?tok))
                                  )
                                  !a
                              : ?a
                            & ( !e
                              . !a,reindex$(!c.!prefix !tok ".")
                              )
                          | ( !e
                            .     (   !e:p
                                    & (n.1+!par:?par)
                                  | 
                                  )
                                  !a
                              ,   reindex
                                $ ( !c
                                  .   !prefix
                                      (   !e:s
                                        & (1+!sen:?sen) "."
                                      | 
                                      )
                                  )
                            )
                          )
                      | !arg
                  )
                . !arg
                )
        )
      & ( makeValidTEI
        =   E1 E2 S Sn
          .   ( glue
              =   E1 E2 T E1s
                .     :?E1:?E1s
                    &   whl
                      ' ( !arg:%?E1 ?arg
                        & (     !E1
                              : (?.? (T.?T) ?,?)
                            &   whl
                              ' (   !arg
                                  :   %( ( ?
                                         .   ? (T.!T) ?
                                           , ?
                                         )
                                       : ?E2
                                       )
                                      ?arg
                                & !E2 !E1:?E1
                                )
                          | 
                          )
                        &     (   !E1:% %
                                & (w.,!E1)
                              | !E1
                              )
                              !E1s
                          : ?E1s
                        )
                    & !E1s
                  | fishy!!!!!!!!!!!!!!!!!!!!!!!
              )
            & :?Sn:?E1
            &   whl
              ' ( !arg:%?E1 ?arg
                & (     !E1
                      : (?.? (S.?S) ?,?)
                    &   whl
                      ' (   !arg
                          :   %( ( (?.? (S.!S) ?,?)
                                 | ~(?.? (S.?) ?,?)
                                 )
                               : ?E2
                               )
                              ?arg
                        & !E2 !E1:?E1
                        )
                  | 
                  )
                &   whl
                  ' ( !E1
                    :   (c.? (type.s) ?,?)
                        ?E1
                    )
                & (.glue$!E1) !Sn:?Sn
                )
            &   map
              $ ( ( 
                  =   
                    .   !arg:(.?arg)
                      & (!arg:|(s.,!arg))
                  )
                . !Sn
                )
        )
      & ( text
        .   
          , ( body
            .   
              ,       vap
                    $ ( ( 
                        =   
                          .   1+!seg:?seg
                            & 0:?tok
                            & 0:?id
                            & (@(!arg:?arg \r)|)
                            & ( p
                              .   
                                ,       vap
                                      $ ( ( 
                                          =   
                                            .   vap$(CUT.!arg.!DEL)
                                                (c.(type.s),)
                                          )
                                        . !arg
                                        . " "
                                        )
                                    : ?SEG
                                  & ( !SEG:
                                    |   !SEG:?SEG (c.?)
                                      & makeValidTEI$!SEG:?SEG
                                      & !SEG
                                    )
                              )
                        )
                      . get$(!arg,STR)
                      . \n
                      )
                  : ?P
                & (!P:?P (p.?,)|)
                & :?Q
                &   whl
                  ' ( !P:(p.?a,?c) ?P
                    & ( !c:
                      |     whl
                          ' (   !P
                              :   ( p
                                  . ?a,?d&!d:~
                                  )
                                  ?P
                            & !d !c:?c
                            )
                        & (p.!a,inv$!c) !Q:?Q
                      )
                    & !Q
                    )
                & 0:?sen:?par
                & reindex$(inv$!Q.i)
            )
        )
  )
  ( new
  =   
    .   ~
      |   ~
        &   put
          $ ( toML$((its.doit)$"text-137-727-step1.plainD")
            , "text-137-727-step1.plainD.out"
            , NEW
            )
      |   ~
        & put$(toML$((its.doit)$F),"F.out",NEW)
      |   ~
        &   put
          $ ( toML$((its.doit)$"text-216-437-step1.bis.plainD")
            , "maak.xml"
            , NEW
            )
      |   put
        $ (toML$((its.doit)$(arg$)),arg$,NEW)
  );

r=
  get'("f2b.bra",TXT)
& rmv$(str$(f2b ".bak"))
& ren$("f2b.bra".str$(f2b ".bak"))
&   put
  $ ( "{f2b.bra

Take segmentized and tokenized text with DEL as separator between 'tokens' that belong to the same input word apart.
Output a <text> element in TEI-P5 style.}

"
    , "f2b.bra"
    , NEW
    , BIN
    )
& lst'(X,"f2b.bra",APP)
& put'(\n,"f2b.bra",APP,BIN)
& lst'(r,"f2b.bra",APP)
& put$(str$("\nnew'" X ";\n"),"f2b.bra",APP,BIN)
& ;

new'X;
