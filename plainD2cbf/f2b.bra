{f2b.bra

Take segmentized and tokenized with DEL as separator between tokens that tear the input word apart.}

X=
  ( doit
  =   
    .   0:?seg
      & chr$127:?DEL
      & ( FF
        =   x y
          .   :?y
            &   whl
              ' ( @( !arg
                   : ?x (%@:(\f|\v):?fv) ?arg
                   )
                &     ( !fv:\f&pb|lb
                      . ,
                      )
                      ( !x:
                      | (w.!wid (S.!seg) (T.!tok),!x)
                      )
                      !y
                  : ?y
                )
            &   ( !arg:
                | (w.!wid (S.!seg) (T.!tok),!arg)
                )
                !y
        )
      & ( cut
        =   W a wid
          .   :?W
            &   whl
              ' ( @(!arg:?a "." ?arg)
                & !ID:?wid
                & FF$!a:?a
                &     ( c
                      .     !ID
                            (S.!seg)
                            (T.!tok)
                            (type.p)
                        , "."
                      )
                      !a
                      !W
                  : ?W
                )
            & !ID:?wid
            & FF$!arg !W
        )
      & ( CUT
        =   WW
          .       vap
                $ ( ( 
                    =   
                      .       !arg
                            : ( "'"
                              | "("
                              | ")"
                              | "["
                              | "]"
                              | "{"
                              | "}"
                              | ":"
                              | ","
                              | "-"
                              | –
                              | —
                              | …
                              | "!"
                              | ‹
                              | ›
                              | «
                              | »
                              | "?"
                              | ‘
                              | ’
                              | “
                              | ”
                              | ";"
                              | "\\"
                              | "/"
                              | \"
                              | "&"
                              )
                          & (p.!arg)
                        | !arg
                    )
                  . !arg
                  )
              : ?C
            &   whl
              ' ( !C:?A (p.%@?x ?y) (p.!x) ?Z
                & !A (p.!x !y !x) !Z:?C
                )
            & :?O
            &   whl
              ' ( !C:?A (p.?x) ?C
                & ( !A:
                  |   1+!tok:?tok
                    & cut$(str$!A):?A
                  )
                &     ( c
                      .     !ID
                            (S.!seg)
                            (T.1+!tok:?tok)
                            (type.p)
                        , str$!x
                      )
                      !A
                      !O
                  : ?O
                )
            & ( !A:
              | str$!C:?C:
              | 1+!tok:?tok&cut$!C !O:?O
              )
            & :?OO
            & whl'(!O:%?A ?O&!A !OO:?OO)
            & !OO
        )
      & ( text
        .   
          , ( body
            .   
              ,       vap
                    $ ( ( 
                        =   
                          .   1+!seg:?seg
                            & 0:?tok
                            & 0:?id
                            & ( ID
                              =   "xml:id"
                                . str$(i !seg "." (1+!id:?id))
                              )
                            & (@(!arg:?arg \r)|)
                            & ( p
                              .   (n.!seg)
                                ,       vap
                                      $ ( ( 
                                          =   
                                            .   vap$(CUT.!arg.!DEL)
                                                (c.!ID (type.s),)
                                          )
                                        . !arg
                                        . " "
                                        )
                                    : ?SEG
                                  & ( !SEG:
                                    |   !SEG:?SEG (c.?)
                                      & !SEG
                                    )
                              )
                        )
                      . get$(!arg,STR)
                      . \n
                      )
                  : ?P
                & (!P:?P (p.?,)|)
                & !P
            )
        )
  )
  ( new
  =   
    .   ~
      |   ~
        & put$(toML$((its.doit)$"FF.je"),"FF.je.xml",NEW)
      |   ~
        & put$(toML$((its.doit)$"maak.txt"),"maak.xml",NEW)
      | put$(toML$((its.doit)$(arg$)),arg$,NEW)
  );

r=
  get'("f2b.bra",TXT)
& rmv$(str$(f2b ".bak"))
& ren$("f2b.bra".str$(f2b ".bak"))
&   put
  $ ( "{f2b.bra

Take segmentized and tokenized with DEL as separator between tokens that tear the input word apart.}

"
    , "f2b.bra"
    , NEW
    , BIN
    )
& lst'(X,"f2b.bra",APP)
& put'(\n,"f2b.bra",APP,BIN)
& lst'(r,"f2b.bra",APP)
& put$(str$("\nnew'" X ";\n"),"f2b.bra",APP,BIN)
& ;

new'X;
