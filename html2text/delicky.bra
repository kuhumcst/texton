{delicky.bra
ğ•Œğ•‹ğ”½-ğŸ 

Remove unwanted elements, such as <a> (but not the link text!)}

X=
  ( doit
  =   
    .   !arg:(?input.?output)
      & nestML$(get$(!input,HT ML)):?html
      & ( rec
        =   e a c
          .   map
            $ ( ( 
                =   
                  .   !arg:@
                    |     !arg
                        : (   img
                            | head
                            | script
                          . ?
                          )
                      & 
                    |     !arg
                        : (sup.?,? (a.?) ?)
                      & 
                    |   !arg:(?e.?a,?c)
                      & (     !e
                            : ( a
                              | div
                              | figure
                              )
                          & rec$!c
                        | (!e.!a,rec$!c)
                        )
                    | 
                )
              . !arg
              )
        )
      & rec$!html:?html
      & put$(str$(toML$!html),!output,NEW,BIN)
  )
  ( new
  =   
    .   ~
      |   ~
        & (its.doit)$("HTMLPage1.html"."HTMLPage2.html")
      | (its.doit)$(arg$.arg$)
  );

r=
  get'("delicky.bra",TXT)
& rmv$(str$(delicky ".bak"))
& ren$("delicky.bra".str$(delicky ".bak"))
&   put
  $ ( "{delicky.bra
ğ•Œğ•‹ğ”½-ğŸ 

Remove unwanted elements, such as <a> (but not the link text!)}

"
    , "delicky.bra"
    , NEW
    , BIN
    )
& lst'(X,"delicky.bra",APP)
& put'(\n,"delicky.bra",APP,BIN)
& lst'(r,"delicky.bra",APP)
& put$(str$("\nnew'" X ";\n"),"delicky.bra",APP,BIN)
& ;

new'X;
