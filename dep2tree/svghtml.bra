{svghtml.bra

Convert conll output from dependency2tree to HTML, combining all .svg files, adding title, and renumbering ids.}

X=
  ( doit
  =   odir lsodir F dep2treefile svgf
    .   !arg:(?odir.?lsodir.?F.?conll.?dep2treefile)
      & get$(!lsodir,STR):?svgs
      & lst$(svgs,svgs,NEW)
      & 0:?graph:?node:?edge
      & get$(!conll,STR):?CONLL
      &     str
          $ ( vap
            $ ( ( 
                =   
                  .     @(!arg:? \t ?arg \t ?)
                      & " " !arg
                    | \n
                )
              . !CONLL
              . \n
              )
            )
        : ?sents
      & 0:?sentNR
      &     vap
          $ ( ( 
              =   
                .     @(!arg:" " ?arg)
                    & (1+!sentNR:?sentNR.!arg)
                  | 
              )
            . !sents
            . \n
            )
        : ?sents
      &     map
          $ ( ( 
              =   svg
                .   !arg
                  ,       map
                        $ ( (=.!arg:~" "|)
                          . get$(str$(!odir "/" !arg),X ML TRM)
                          )
                      : ? (svg.?att) ?svg
                    & (   !att:?a (width.?) ?z
                        & !a !z:?att
                      | 
                      )
                    & (   !att:?a (height.?) ?z
                        & !a !z:?att
                      | 
                      )
                    & (svg.!att) !svg:?svg
                    &   map
                      $ ( ( 
                          =   a z e id
                            .       !arg
                                  : ( ?e
                                    . ?a (id.@(?:?id #)) ?z
                                    )
                                & ( !e
                                  .   !a
                                      (id.str$(!id (!!id+1:?!id)))
                                      !z
                                  )
                              | !arg
                          )
                        . !svg
                        )
              )
            .   vap
              $ ( (=.@(!arg:D ?)|)
                . !svgs
                . \n
                )
            )
        : ?SVGS
      &     ("!DOCTYPE"." html")
            (html.(lang.en) (xmlns."http://www.w3.org/1999/xhtml"))
            (head.)
            (meta.(charset.utf-8),)
            (title.)
            BLA
            (.title.)
            (.head.)
            (body.)
              map
            $ ( ( 
                =   
                  .   !arg:(?,?arg)
                    & !sents:(?sentNR.?sent) ?sents
                    & (p.,!sentNR) (p.,!sent) !arg
                )
              . !SVGS
              )
            (.body.)
            (.html.)
        : ?html
      & nestML$!html:?html
      & put$(toML$!html,!dep2treefile,NEW)
  )
  ( new
  =   
    .   ~
      |   arg$:?odir
        & arg$:?lsodir
        & arg$:?F
        & arg$:?conll
        & arg$:?dep2treefile
        & (its.doit)$(!odir.!lsodir.!F.!conll.!dep2treefile)
  );

r=
  get'("svghtml.bra",TXT)
& rmv$(str$(svghtml ".bak"))
& ren$("svghtml.bra".str$(svghtml ".bak"))
&   put
  $ ( "{svghtml.bra

Convert conll output from dependency2tree to HTML, combining all .svg files, adding title, and renumbering ids.}

"
    , "svghtml.bra"
    , NEW
    , BIN
    )
& lst'(X,"svghtml.bra",APP)
& put'(\n,"svghtml.bra",APP,BIN)
& lst'(r,"svghtml.bra",APP)
& put$(str$("\nnew'" X ";\n"),"svghtml.bra",APP,BIN)
& ;

new'X;
