{svghtml.bra

Convert conll output from dependency2tree to HTML, combining all .svg files, adding title, and renumbering ids.}

X=
  ( doit
  =     odir lsodir F dep2treefile svgs modulus
      , AllSVGs
    .     !arg
        : ( ?F
          . ?conll
          . ?dep2treefile
          . ?modulus
          . ?odir
          . ?lastsubdir
          . ?remainder
          )
      & ( getSVG
        =   svg
          .   !arg
            ,       map
                  $ ( (=.!arg:~" "|)
                    . get$(!arg,X ML TRM)
                    )
                : ? (svg.?att) ?svg
              & (   !att:?a (width.?) ?z
                  & !a !z:?att
                | 
                )
              & (   !att:?a (height.?) ?z
                  & !a !z:?att
                | 
                )
              & (svg.!att) !svg:?svg
              &   map
                $ ( ( 
                    =   a z e id
                      .       !arg
                            : ( ?e
                              .   ?a
                                  (id.@(?:?id #))
                                  ?z
                              )
                          & ( !e
                            .   !a
                                ( id
                                . str$(!id (!!id+1:?!id))
                                )
                                !z
                            )
                        | !arg
                    )
                  . !svg
                  )
        )
      & ( AllSVGs
        =   modulus lastsubdir remainder RET subdir N
          .   !arg:(?modulus.?odir.?lastsubdir.?remainder)
            & :?RET
            & !lastsubdir:?subdir
            & !remainder:?N
            &   whl
              ' (   whl
                  ' (       getSVG
                          $ ( str
                            $ ( !odir
                                "/"
                                !subdir
                                "/D-"
                                ( !N:<10&00
                                | !N:<100&0
                                | 
                                )
                                !N
                                ".svg"
                              )
                            )
                          !RET
                      : ?RET
                    & !N+-1:>0:?N
                    )
                & !modulus:?N
                & !subdir+-1:~<0:?subdir
                )
            & !RET
        )
      &   AllSVGs$(!modulus.!odir.!lastsubdir.!remainder)
        : ?SVGS
      & 0:?graph:?node:?edge
      & get$(!conll,STR):?CONLL
      &     str
          $ ( vap
            $ ( ( 
                =   
                  .     @(!arg:? \t ?arg \t ?)
                      & " " !arg
                    | \n
                )
              . !CONLL
              . \n
              )
            )
        : ?sents
      & 0:?sentNR
      &     vap
          $ ( ( 
              =   
                .     @(!arg:" " ?arg)
                    & (1+!sentNR:?sentNR.!arg)
                  | 
              )
            . !sents
            . \n
            )
        : ?sents
      &     ("!DOCTYPE"." html")
            ( html
            .   (lang.en)
              ,   ( head
                  .   
                    ,   (meta.(charset.utf-8),)
                        (title.,dependency2tree)
                  )
                  ( body
                  .   
                    ,   map
                      $ ( ( 
                          =   
                            .   !arg:(?,?arg)
                              &   !sents
                                : (?sentNR.?sent) ?sents
                              &   ( p
                                  .   
                                    ,   ( strong
                                        . ,str$("#" !sentNR ": ")
                                        )
                                        !sent
                                  )
                                  !arg
                          )
                        . !SVGS
                        )
                  )
            )
        : ?html
      & nestML$!html:?html
      & put$(toML$!html,!dep2treefile,NEW)
  )
  ( new
  =   
    .   ~
      |   arg$:?F
        & arg$:?conll
        & arg$:?dep2treefile
        & arg$:?modulus
        & arg$:?odir
        & arg$:?lastsubdir
        & arg$:?remainder
        &   (its.doit)
          $ ( !F
            . !conll
            . !dep2treefile
            . !modulus
            . !odir
            . !lastsubdir
            . !remainder
            )
  );

r=
  get'("svghtml.bra",TXT)
& rmv$(str$(svghtml ".bak"))
& ren$("svghtml.bra".str$(svghtml ".bak"))
&   put
  $ ( "{svghtml.bra

Convert conll output from dependency2tree to HTML, combining all .svg files, adding title, and renumbering ids.}

"
    , "svghtml.bra"
    , NEW
    , BIN
    )
& lst'(X,"svghtml.bra",APP)
& put'(\n,"svghtml.bra",APP,BIN)
& lst'(r,"svghtml.bra",APP)
& put$(str$("\nnew'" X ";\n"),"svghtml.bra",APP,BIN)
& ;

new'X;
