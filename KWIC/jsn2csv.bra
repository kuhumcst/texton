{jsn2csv.bra

Convert output from Korp to a tabulator separated fomat that can be imported in Excel.

rfc2396:

reserved    = ; / ? : @ & = + $ ,
unreserved  = alphanum | mark
mark        = - _ . ! ~ * ' ( )
control     = US-ASCII coded characters 00-1F and 7F hexadecimal
space       = US-ASCII coded character 20 hexadecimal
delims      = < > # % "
unwise      = {  }  |   ^  [  ]  `

}

X=
  ( doit
  =   url
    .   @(!arg:?url "?" ?arg)
      & ( percentencode
        =   
          .   str
            $ ( map
              $ ( ( 
                  =   
                    .       !arg
                          : ( ";"
                            | "/"
                            | "?"
                            | ":"
                            | "@"
                            | "&"
                            | "="
                            | "+"
                            | "$"
                            | ","
                            | "%"
                            | " "
                            | "<"
                            | ">"
                            | "#"
                            | "%"
                            | \"
                            | "{"
                            | "}"
                            | "|"
                            | "^"
                            | "["
                            | "]"
                            | "`"
                            )
                        & "%" d2x$(asc$!arg)
                      |   asc$!arg:127
                        & "%" d2x$(asc$!arg)
                      | !arg
                  )
                . get$(str$!arg,MEM,VAP)
                )
              )
        )
      & ( percentdecode
        =   A a b Z
          .     @(!arg:?A "%" %@?a %@?b ?Z)
              & !A chr$(x2d$(str$(!a !b))) percentdecode$!Z
            | !arg
        )
      & ( plusdecode
        =   A Z
          .   @(!arg:?A "+" ?Z)&!A " " plusdecode$!Z
            | !arg
        )
      &     vap
          $ ( ( 
              =   
                .     @(!arg:?K "=" ?V)
                    &     start
                          end
                          context
                          cache
                          defaultcontext
                          corpus
                          cqp
                          show
                          search
                          "default_within"
                          within
                      : ? !K ?
                    & (!K.!V)
                  | 
              )
            . !arg
            . "&"
            )
        : ?parms
      & lst$(parms,parms0,NEW)
      &   (   !parms:?A (end.?) ?Z
            & !A (end.999) !Z
          | (end.999) !parms
          )
        : ?parms
      & lst$(parms,parms1,NEW)
      & ( !parms:?A (start.?) ?Z
        | (start.0) !parms:?parms
        )
      & lst$(parms,parms2,NEW)
      & ( translate
        =   A Z
          .   !arg:?A (cqp.?cqp) ?Z
            & ( trans
              =   A Z M U
                .     @(!arg:?A "_=" ?M \" ?U \" ?Z)
                    & str$(!A "=" !M "\".*" !U ".*\"" !Z)
                  |   @(!arg:?A "^=" ?M \" ?U \" ?Z)
                    & str$(!A "=" !M \" !U ".*\"" !Z)
                  |   @(!arg:?A "&=" ?M \" ?U \" ?Z)
                    & str$(!A "=" !M "\".*" !U \" !Z)
                  | !arg
              )
            & !A (cqp.trans$!cqp) !Z
        )
      & ( !parms:? (show.?) ?
        | !parms (show.pos%2Cmsd%2Clemma):?parms
        )
      & lst$(parms,parms3,NEW)
      & ( !parms:? ("show_struct".?) ?
        |     !parms
              ( "show_struct"
              . "text_title%2Ctext_Instrument%2Ctext_Platform%2Ctext_Mediatype%2Ctext_Original%2Ctext_Domain%2Ctext_Exactdate%2Ctext_Notbeforedate%2Ctext_Notafterdate%2Ctext_Origplace%2Ctext_Juridoutcome%2Ctext_SenderTargetrelation%2Ctext_Sendertype%2Ctext_SenderID%2Ctext_Senderage%2Ctext_Sendergender%2Ctext_Victimtype%2Ctext_VictimID%2Ctext_Victimage%2Ctext_Victimgender%2Csentence_id%2Cparagraph_id"
              )
          : ?parms
        )
      & lst$(parms,parms4,NEW)
      & ( !parms:? ("default_within".?) ?
        | !parms ("default_within".sentence):?parms
        )
      & lst$(parms,parms5,NEW)
      & ( !parms:? ("default_context".?) ?
        | !parms ("default_context".1%20sentence):?parms
        )
      & lst$(parms,parms6,NEW)
      & (!parms:?A (within.?) ?Z&!A !Z:?parms|)
      & (   ~
          & !parms:? (corpus.?corpus) ?
          &   vap$((=."," upp$!arg ":text").!corpus.",")
            : "," ?within
          & !parms (within.!within):?parms
        | !parms (within.text):?parms
        )
      & (   !parms:?A (context.?) ?Z
          & !A !Z:?parms
        | 
        )
      & lst$(parms,parms7,NEW)
      &     map
          $ ( ( 
              =   key value
                .   !arg:(?key.?value)
                  & (!key.str$(percentdecode$(plusdecode$!value)))
              )
            . !parms
            )
        : ?parms
      & lst$(parms,parms8a,NEW)
      & (   !parms:? (corpus.?corpus) ?
          &   vap$((=."," upp$!arg ":text").!corpus.",")
            : "," ?context
          & !parms (context.!context):?parms
        | !parms (context.text):?parms
        )
      & lst$(parms,parms8b,NEW)
      & (   !parms:?A (search.?s) ?Z
          & !A !Z:?parms
          & ( !s:cqp&translate$!parms:?parms
            |   @(!s:?k "|" ?v)
              & (cqp."[" !k " = \"" !v \"]):?cqp
              &   (   !parms:?A (cqp.?) ?Z
                    & !A !cqp !Z
                  | !parms !cqp
                  )
                : ?parms
            )
        | 
        )
      & lst$(parms,parms9,NEW)
      &     map
          $ ( ( 
              =   key value
                .   !arg:(?key.?value)
                  & (!key.str$(percentencode$!value))
              )
            . !parms
            )
        : ?parms
      & lst$(parms,parms10,NEW)
      &     str
          $ ( "https://alf.hum.ku.dk/korp/"
              ( @(!url:? korp-old ?)&"cgi/korp.cgi"
              | backend/query
              )
              "?command=query"
                map
              $ ( ( 
                  = .!arg:(?K.?V)&"&" !K "=" !V
                  )
                . !parms
                )
            )
        : ?Q
      & lst$(Q,Q,NEW)
      &   ( ~&"C:\\texlive\\2020\\tlpkg\\installer\\wget\\wget.exe"
          | wget
          )
        : ?wget
      & str$(!wget " -O T.jsn \"" !Q \"):?command
      & sys$!command
      & get$("T.jsn",JSN):?jsn
      & lst$(jsn,jsn,NEW)
      & !jsn:(?+(kwic.,?kwic)+?,)
      & 0:?N
      & 0:?maxstart
      & 0:?maxendpos
      &   map
        $ ( ( 
            =   
              .     !arg
                  : (   ?+(match.?+(start.?start)+?,)+?
                      : ?+(tokens.,? [?ntokens)+?
                    , 
                    )
                & (!start:>!maxstart:?maxstart|)
                & !ntokens+-1*!start:>!maxendpos:?maxendpos
            )
          . !kwic
          )
      & -1*!maxstart:?maxstart
      & ( tab
        =   
          .   !arg:<0&\t tab$(1+!arg)
            | 
        )
      & ( countup
        =   a b
          .   !arg:(?a.?b)
            &   \t
                !a
                ( !a+1:<!b&countup$(!a+1.!b)
                | 
                )
        )
      & ( head
        =   cell
          .   !arg:(?+(structs.?structs,)+?,) ?
            & ( cell
              =   k
                .     !arg:(?k..?)+?arg
                    & \t !k cell$!arg
                  | 
              )
            & cell$!structs:\t ?cells
            & !cells countup$(!maxstart.!maxendpos) \n
        )
      & ( empty
        =   cell
          .   !arg:(?+(structs.?structs,)+?,) ?
            & ( cell
              =   k
                .     !arg:(?k..?)+?arg
                    & \t cell$!arg
                  | 
              )
            & cell$!structs:\t ?cells
            & !cells
        )
      & ( meta
        =   
          .   !arg:(?+(structs.?structs,)+?,)
            & ( cell
              =   
                .     !arg:(?..?v)+?arg
                    & \t !v cell$!arg
                  | 
              )
            & cell$!structs:\t ?cells
            & !cells
        )
      & empty$!kwic:?emp
      &     head$!kwic
              map
            $ ( ( 
                =   
                  .     !arg
                      : (   ?+(match.?+(start.?start)+?,)+?
                          : ?+(tokens.,?tokens)+?
                        , 
                        )
                    &   meta$!arg
                        (tab$(!maxstart+!start):?tabs)
                          map
                        $ ( ( 
                            =   
                              .   !arg:(?+(word..?word)+?,)
                                & \t !word
                            )
                          . !tokens
                          )
                        \n
                        !emp
                        !tabs
                          map
                        $ ( ( 
                            =   
                              .   \t
                                  (     !arg
                                      : (?+(lemma..?lemma)+?,)
                                    & !lemma
                                  | 
                                  )
                            )
                          . !tokens
                          )
                        \n
                        !emp
                        !tabs
                          map
                        $ ( ( 
                            =   
                              .   \t
                                  (   !arg:(?+(msd..?msd)+?,)
                                    & !msd
                                  | 
                                  )
                            )
                          . !tokens
                          )
                        \n
                )
              . !kwic
              )
        : ?output
      & put$(str$!output,!outputfile,NEW)
  )
  ( new
  =   outputfile
    .   output:?outputfile
      & (   ~
          &   (its.doit)
            $ "https://alf.hum.ku.dk/korp/?mode=threats#?lang=en&stats_reduce=word&corpus=threatsjeb&cqp=%5Bmsd%20%26%3D%20%22PLU%22%5D&search_tab=1&search=cqp"
        |   ~
          &   (its.doit)
            $ "https://alf.hum.ku.dk/korp/?mode=threats#?lang=en&stats_reduce=word&corpus=threatsjeb&cqp=%5Bmsd%20_%3D%20%222%22%5D&search_tab=1&search=cqp"
        |   ~
          &   (its.doit)
            $ "https://alf.hum.ku.dk/korp/?mode=threats#?stats_reduce=word&corpus=threatsjeb&cqp=%5Blemma%20%3D%20%22rette%22%5D&search_tab=1&search=cqp"
        |   ~
          &   (its.doit)
            $ "https://alf.hum.ku.dk/korp/?mode=threats#?lang=en&start=0&end=999&context=&cache=true&defaultcontext=1%20sentence&corpus=threatsjeb&cqp=%5Bmsd%20%3D%20%22PRON.PERS.2.SING%22%5D&show=sentence,pos,msd,lemma,ref,prefix,suffix&stats_reduce=word&sort=random&random_seed=5002506&search_tab=1&search=cqp"
        |   ~
          &   (its.doit)
            $ "https://alf.hum.ku.dk/korp/?mode=threats#?lang=en&start=0&end=999&context=&cache=true&defaultcontext=1%20sentence&corpus=threatsjeb&cqp=%5Bmsd%20%3D%20%22PRON.PERS.1.SING%22%5D&show=sentence,pos,msd,lemma,ref,prefix,suffix&stats_reduce=word&sort=random&random_seed=5002506&search=word%7Csender&page=0"
        |   ~
          &   (its.doit)
            $ "https://alf.hum.ku.dk/korp/?mode=threats#?lang=en&start=0&end=999&context=&cache=true&defaultcontext=1%20sentence&corpus=threatsjeb&cqp=%5Bmsd%20%3D%20%22PRON.PERS.1.SING%22%5D&show=sentence,pos,msd,lemma,ref,prefix,suffix&stats_reduce=word&sort=random&random_seed=5002506&search_tab=1&search=cqp"
        |   ~
          &   (its.doit)
            $ "https://alf.hum.ku.dk/korp/?mode=threats#?lang=en&start=0&end=999&context=&cache=true&defaultcontext=1%20sentence&corpus=threatsjeb&cqp=%5Bword%20%3D%20%22jeg%22%20%26%20(lemma%20%3D%20%22jeg%22%20%7C%20_.text_Juridoutcome%20%3D%20%22juridical%22)%5D&show=sentence,pos,msd,lemma,ref,prefix,suffix&stats_reduce=word&sort=random&random_seed=5002506&search_tab=1&search=cqp"
        |   ~
          &   (its.doit)
            $ "https://alf.hum.ku.dk/korp-old/?mode=threats#?lang=en&stats_reduce=word&corpus=threatsjeb&cqp=%5Bmsd%20_%3D%20%222%22%5D&search_tab=1&within=text&search=cqp"
        |   get$(arg$,STR):?F
          & arg$:?outputfile
          & ( @(!F:?F (\n|\r|" ") ?)
            | 
            )
          & lst$(F,F,NEW)
          & (its.doit)$!F
        )
  );

r=
  get'("jsn2csv.bra",TXT)
& rmv$(str$(jsn2csv ".bak"))
& ren$("jsn2csv.bra".str$(jsn2csv ".bak"))
&   put
  $ ( "{jsn2csv.bra

Convert output from Korp to a tabulator separated fomat that can be imported in Excel.

rfc2396:

reserved    = ; / ? : @ & = + $ ,
unreserved  = alphanum | mark
mark        = - _ . ! ~ * ' ( )
control     = US-ASCII coded characters 00-1F and 7F hexadecimal
space       = US-ASCII coded character 20 hexadecimal
delims      = < > # % \"
unwise      = {  }  |   ^  [  ]  `

}

"
    , "jsn2csv.bra"
    , NEW
    , BIN
    )
& lst'(X,"jsn2csv.bra",APP)
& put'(\n,"jsn2csv.bra",APP,BIN)
& lst'(r,"jsn2csv.bra",APP)
& put$(str$("\nnew'" X ";\n"),"jsn2csv.bra",APP,BIN)
& ;

new'X;
